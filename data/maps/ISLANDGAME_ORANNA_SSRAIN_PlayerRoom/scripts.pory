mapscripts ISLANDGAME_ORANNA_SSRAIN_PlayerRoom_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (!flag(FLAG_RECEIVED_RUNNING_SHOES)) {
            setvar(VAR_TEMP_1, 7)
        }

        // prize for helping arthur and his sister
        if (!flag(FLAG_ORANNA_ROSEVALE_AMULET_COIN)) {
            setflag(FLAG_TEMP_1)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_1, 7: ISLANDGAME_ORANNA_SSRAIN_PlayerRoom_GameIntro
    ]
}

script ISLANDGAME_ORANNA_SSRAIN_PlayerRoom_GameIntro {
    lockall
    playse(SE_DING_DONG)
    waitse
    setvar(VAR_TEMP_1, 3)
    msgbox(format(
        "Ladies and gentlemen, this is your devilishly handsome captain speaking!\p"
        "I'm delighted to announce that we've finally reached our destination!\p"
        "On behalf of the entire crew, I'd like to thank you for choosing to sail with us.\p"
        "Now, go out there and explore all that the Maryan Isles has to offer!\p"
        "We'll be patiently waiting here for your return! Until next time, buh-bye!"
    ))
    closemessage
    releaseall    
}


script ISLANDGAME_ORANNA_SSRAIN_PlayerRoom_PikaDoll {
    lock
    msgbox(format(
        "{COLOR RED}(It's the Pikachu Doll I got when I was only six years old.)\p"
        "(I can't believe it's already been ten years...)"
    ))
    closemessage
    release
}

script ISLANDGAME_ORANNA_SSRAIN_PlayerRoom_AzuDoll {
    lock
    msgbox(format(
        "{COLOR RED}(It's the Azurill Doll I got from Arthur's Sister during the night I stayed over at their inn.)\p"
        "(It's not shiny like hers, though.)"
    ))
    closemessage
    release
}

script ISLANDGAME_ORANNA_SSRAIN_PlayerRoom_NeedShoes {
    if (!flag(FLAG_RECEIVED_RUNNING_SHOES)) { # Run trigger script if you have not recieved the running shoes
        lockall
        msgbox(format(
            "{COLOR RED}(I should probably get my shoes before I leave.)"
        ))
        closemessage
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
        waitmovement(0)
    }
    releaseall
}

script ISLANDGAME_ORANNA_SSRAIN_PlayerRoom_RunningShoes {
    lockall
    setflag(FLAG_RECEIVED_RUNNING_SHOES)
    msgbox(format(
        "{COLOR GREEN}You changed into your Running Shoes! Press “B” to run!\p"
        "You can also press the “R” button to toggle auto-running!"
    ))
    removeobject(VAR_LAST_TALKED)
    setflag(FLAG_SYS_B_DASH)
    releaseall
}