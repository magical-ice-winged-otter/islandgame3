const FLAG_DAYTIME = FLAG_TEMP_2
const FLAG_NIGHTTIME = FLAG_TEMP_3

mapscripts ISLANDGAME_ORANNA_SSRAIN_Cafeteria_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {  
        gettime

        if (var(VAR_0x8000) < 6 || var(VAR_0x8000) >= 18) {
            setflag(FLAG_NIGHTTIME) // it is night
        }
        else {
            setflag(FLAG_DAYTIME) // it is day
        }
    }
}

// disgruntled tourist
script ISLANDGAME_ORANNA_SSRAIN_Cafeteria_NPC1 {
    lock
    faceplayer
    msgbox(format(
        "I can't believe meals aren't paid for with the ticket.\p"
        "These prices are highway robbery!"
    ))
    closemessage
    release
}

// cook
script ISLANDGAME_ORANNA_SSRAIN_Cafeteria_NPC2 {
    lock
    faceplayer
    msgbox(format(
        "Contrary to popular belief, our famous lobster roll is not made from Pokémon.\p"
        "We actually just use regular ol' lobster.\p"
        "Yeah, you heard that right. Normal animals exist in the Pokémon world.\p"
        "Now you can stop accusing me of committing Pokémon cruelty!"
    ))
    closemessage
    release
}

// alcremie
script ISLANDGAME_ORANNA_SSRAIN_Cafeteria_Alcremie {
    lock
    msgbox(format(
        "{COLOR RED}(Despite its appearance, it's just a cake that looks like an Alcremie.)"
    ))
    closemessage
    release
}

// ice cream kid
script ISLANDGAME_ORANNA_SSRAIN_Cafeteria_IceCreamGirl {
    lock
    faceplayer
    if(!flag(FLAG_ORANNA_SSRAIN_ICECREAM)) {
        msgbox(format("I don't like how this ice cream tastes... Do you want it?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == NO) {
            msgbox(format("But I don't want it!"))
            goto(IceCreamGirl_End)
        }
        giveitem(ITEM_CASTELIACONE)
        setflag(FLAG_ORANNA_SSRAIN_ICECREAM)
    }
    msgbox(format("I wanted orange sher-bet, but they don't have it here..."))

    IceCreamGirl_End:
    closemessage
    release
}

// lobster roll seller
script ISLANDGAME_ORANNA_SSRAIN_Cafeteria_LobsterRollGuy {
    lock
    faceplayer
    if (flag(FLAG_NIGHTTIME)) {
        if (!flag(FLAG_DAILY_ORANNA_SSRAIN_LOBSTER_ROLL)) {
            msgbox(format(
                "Our famous lobster roll is made with only the freshest lobster."
            ))
            showmoneybox(0,0)
            msgbox(format(
                "Would you like to try one? It is ¥3000 for a roll."
            ), MSGBOX_YESNO)
            if (var(VAR_RESULT) == YES) {
                checkmoney(3000)
                if (!var(VAR_RESULT)) {
                    msgbox(format(
                        "Sorry, you do not have enough money."
                    ))
                    hidemoneybox
                    goto(LobsterRoll_End)
                }
                else {
                    removemoney(3000)
                    updatemoneybox
                    playse(SE_SHOP)
                    waitse
                    hidemoneybox
                    giveitem(ITEM_ISLANDGAME_LOBSTER_ROLL)
                    setflag(FLAG_DAILY_ORANNA_SSRAIN_LOBSTER_ROLL)
                    goto(LobsterRoll_End)
                }
            }
            else {
                hidemoneybox
                goto(LobsterRoll_End)
            }

        }
        else {
            msgbox(format(
                "Sorry, due to the high popularity of our lobster roll, only one may be purchased per customer every night.\p"
                "If you would like another one, please come back tomorrow."
            ))
        }
    }
    else {
        msgbox(format(
            "If you would like to try our famous lobster roll, you will have to come back later tonight."
        ))
    }

    LobsterRoll_End:
        closemessage
        release
}



