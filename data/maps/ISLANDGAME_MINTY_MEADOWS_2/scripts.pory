mapscripts ISLANDGAME_MINTY_MEADOWS_2_MapScripts {
    // This hides overworld mons smoothly right before the overworld loads
    MAP_SCRIPT_ON_RESUME {
        call(Common_TryRemoveTemporaryObject)
    }

    MAP_SCRIPT_ON_TRANSITION {
        // drifloon can only appear at night
        if (gettimeofday != TIME_NIGHT) {
            setobjecthidden(LOCALID_DRIFLOON)
        }

        // The extra mareeps are only visible when battling the rival
        if (var(VAR_MINTY_MEADOWS_STATE) != MINTY_MEADOWS_BATTLING_RIVAL) {
            call(ISLANDGAME_MINTY_MEADOWS_2_HideExtraMareep)
        }

        switch (var(VAR_MINTY_MEADOWS_STATE)) {
        case MINTY_MEADOWS_SEARCHING_FOR_MAREEP: 
            setobjectxyperm(LOCALID_OLD_MAN, 10, 9)
            setobjectxyperm(LOCALID_RIVAL, 34, 10)
            setobjectmovementtype(LOCALID_RIVAL, MOVEMENT_TYPE_LOOK_AROUND)
        case MINTY_MEADOWS_BATTLING_MIGHTYENA: 
            setobjectxyperm(LOCALID_OLD_MAN, 10, 9)
            setobjectxyperm(LOCALID_RIVAL, 66, 18)
            setobjectmovementtype(LOCALID_RIVAL, MOVEMENT_TYPE_FACE_DOWN)
            setobjectxyperm(LOCALID_MIGHTYENA, 61, 15)
        case MINTY_MEADOWS_SAVED_MAREEP:
            setobjecthidden(LOCALID_RIVAL)
            setobjecthidden(LOCALID_OLD_MAN)
            setobjecthidden(LOCALID_MAREEP)
            setobjecthidden(LOCALID_MIGHTYENA)
        }
    }
}

// section: misc

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight {
    applymovement(LOCALID_RIVAL, moves(
        face_up,
        delay_16, delay_4,
        face_right,
        delay_8 * 3,
        face_left,
        delay_8 * 3,
        face_down,
        delay_16,
        face_right,
        delay_8 * 3,
        face_up,
        delay_16,
        walk_in_place_faster_up,
        delay_16 * 2
    ))

    waitmovement

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Nnngh...{PAUSE 30} I'm only missing one more Pokémon!{PAUSE 30} Where in the world is it?\p"
        "Waah! I give up! I'm convinced it doesn't even exist! \p"
        "Eh?"
    ), MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_RIVAL, moves(emote_question_mark, face_down, delay_8 * 6))
	waitmovement
	playbgm(MUS_HG_LYRA,TRUE)
    applymovement(LOCALID_RIVAL, moves(walk_left * 2, walk_down * 2))
    waitmovement
    msgbox(format("Hiya there!{PAUSE 30}... Err...{PAUSE 30} umm,{PAUSE 30} have we met before?"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_up, delay_16 * 2))
    waitmovement
    playse(SE_PIN)
	applymovement(LOCALID_RIVAL, moves(emote_exclamation_mark, delay_8 * 6)) 
	waitmovement

    msgbox(format(
        "Ooooh! You're {PLAYER}? The professor told me about you earlier today!\p"
        "It's good to meet you! I'm Melissa, a rising Pokémon trainer just like you!\p"
        "I was trying to fill out my Pokédex here, but I couldn't find a Scorbunny, no matter how hard I looked...\p"
        "Hope the isles are treating you kindly so far! I've been here all my life, so if ya ever need guidance, leave it to me!\p"
        "Oh!{PAUSE 30} Now that you're here, let's have a Pokémon battle! I need some action!\p"
        "I might be new just like you... but don't you underestimate me!"
    ), MSGBOX_AUTOCLOSE)

    trainerbattle_no_intro(TRAINER_MINTY_MEADOWS_RIVAL, format("Ack! Looks like I was the one doing the underestimating!"))

    if (specialvar(VAR_RESULT, GetBattleOutcome) != B_OUTCOME_WON) {
        return
    }

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Drats, I wasn't expecting to lose to you, {PLAYER}... \p"
        "W-Wait, I didn't mean it like that! I was just surprised at how skilled you are!\p"
        "Sorry... I say things before I speak a lot, ehe..."
    ), MSGBOX_AUTOCLOSE)

    setflag(FLAG_MINTY_MEADOWS_RIVAL_DEFEATED)
	savebgm(MUS_DUMMY)
	fadedefaultbgm
    call(ISLANDGAME_MINTY_MEADOWS_2_HideExtraMareep)
    addobject(LOCALID_OLD_MAN)
    setobjectxy(LOCALID_OLD_MAN, 18, 12)
    msgbox(format("Excuse me! Young trainers!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_OLD_MAN, moves(walk_left * 8, walk_up * 3))
    copyobjectxytoperm(LOCALID_OLD_MAN)
    waitmovement
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    waitmovement

    msgbox(format(
        "{COLOR GREEN}Farm Owner: {COLOR DARK_GRAY}I'm Paul, I'm one of the owners of Fresa Farms.\p"
        "Have you two seen a Mareep with a blue collar wandering around by chance? \p"
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}I haven't... Did you lose one?\p"
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}I'm afraid so... When I was counting my Mareep after taking them out to graze, I noticed one was missing."
    ), MSGBOX_AUTOCLOSE)

    playse(SE_PIN)
	applymovement(LOCALID_RIVAL, moves(emote_exclamation_mark, delay_8 * 6)) 
	waitmovement
    msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Oh my... that's terrible! We'll help you look, right {PLAYER}?"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up, delay_16 * 2, walk_in_place_faster_up, delay_16 * 2, face_down, delay_16 * 2))
    waitmovement

    msgbox(format(
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}Really? Oh, I can't thank you two enough! \p"
        "The last place I saw my precious Mareep was in the forest to the east."
    ), MSGBOX_AUTOCLOSE)

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    waitmovement
    msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Sounds good! {PLAYER}! Let's head over there immediately!"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(LOCALID_RIVAL, moves(walk_fast_left, walk_fast_down * 4, walk_fast_right * 9))
    waitmovement
    setobjectxy(LOCALID_RIVAL, 34, 10)
    copyobjectxytoperm(LOCALID_RIVAL)
    special(HealPlayerParty)
    setvar(VAR_MINTY_MEADOWS_STATE, MINTY_MEADOWS_SEARCHING_FOR_MAREEP)
}

script ISLANDGAME_MINTY_MEADOWS_2_HideExtraMareep {
    setobjecthidden(LOCALID_MAREEP_EXTRA1)
    setobjecthidden(LOCALID_MAREEP_EXTRA2)
    setobjecthidden(LOCALID_MAREEP_EXTRA3)
    return
}

// section: interactions

script ISLANDGAME_MINTY_MEADOWS_2_Interact_OldMan {
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
    case MINTY_MEADOWS_SEARCHING_FOR_MAREEP:
        msgbox(format("I could've sworn it was somewhere in the forest to the east..."), MSGBOX_NPC)
    case MINTY_MEADOWS_BATTLING_MIGHTYENA:
        msgbox(format(
            "What? A wild Mightyena was chasing my precious Mareep?\p"
            "You have to save him, please!"
        ), MSGBOX_NPC)
    }
}

script ISLANDGAME_MINTY_MEADOWS_2_Interact_Rival {
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
    case MINTY_MEADOWS_SEARCHING_FOR_MAREEP:
        msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Hmm... where could it be?"), MSGBOX_NPC)
    case MINTY_MEADOWS_BATTLING_MIGHTYENA:
        msgbox(format("We need to save that Mareep!"), MSGBOX_NPC)
    }
}

script ISLANDGAME_MINTY_MEADOWS_2_Interact_Mareep {
    playmoncry(SPECIES_MAREEP, CRY_MODE_WEAK)
    msgbox(format("Reep! Reep! {COLOR BLUE}(BRO HELP ME PLZ)"), MSGBOX_AUTOCLOSE)
    waitmoncry
}

script ISLANDGAME_MINTY_MEADOWS_2_Interact_Mightyena {
    faceplayer
    setflag(FLAG_SMART_WILD_AI)
    setflag(FLAG_NO_CATCHING)
    setflag(FLAG_NO_RUNNING)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)

    createmon(B_SIDE_OPPONENT, B_FLANK_LEFT, SPECIES_SHADOW_MIGHTYENA, 
        25, ITEM_SITRUS_BERRY, NUM_NATURES, 0, MON_GENDERLESS, 
        252, 252, 252, 252, 252, 252, 
        31, 31, 31, 31, 31, 31, 
        MOVE_SNARL, MOVE_POISON_FANG, MOVE_SAND_ATTACK, MOVE_SWAGGER)

    setwilddoubleflag
    playmoncry(SPECIES_MIGHTYENA, CRY_MODE_ENCOUNTER)
    waitmoncry
    dowildbattle
    clearflag(FLAG_SMART_WILD_AI)
    clearflag(FLAG_NO_CATCHING)
    clearflag(FLAG_NO_RUNNING)
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)

    if (specialvar(VAR_RESULT, GetBattleOutcome) == B_OUTCOME_RAN) {
        end
    }

    unsetfollowernpc(LOCALID_RIVAL)
    // setobjectmovementtype(LOCALID_RIVAL,MOVEMENT_TYPE_FACE_DOWN)

    if (specialvar(VAR_RESULT, GetBattleOutcome) == B_OUTCOME_LOST) {
        end
    }

    setvar(VAR_MINTY_MEADOWS_STATE, MINTY_MEADOWS_SAVED_MAREEP)


    setvar(VAR_0x8000, 61)
    setvar(VAR_0x8001, 16)
    callnative(TeleportCamera) // this is some magic, shoutout to the pret discord (and see src/islandgame_scrcmd.c)
    applymovement(LOCALID_RIVAL, Common_Movement_FaceUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 61, 16)
    setobjectxy(LOCALID_RIVAL, 60, 16)


    msgbox(format(
        "{COLOR RED}(That Mightyena...{PAUSE 30} Something seemed off about it.)\p"
        "{COLOR RED}(It had some sort of dark aura... radiating from it?)"
    ), MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_RIVAL, moves(walk_in_place_slow_up))
    waitmovement
    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Phew... That was an intense fight. Thank goodness we came in time!"
    ), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_RIVAL, moves(walk_up * 3, face_right))
    waitmovement
    msgbox(format(
        "It looks like Mareep isn't hurt at all. That's a relief..."
    ), MSGBOX_AUTOCLOSE)

    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    applymovement(LOCALID_MAREEP, moves(face_player, jump_in_place_left * 3))
    waitmovement

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Awh. It's okay. You're safe with us now!"
    ), MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_RIVAL, moves(face_down))
    waitmovement
    msgbox(format("Ehe...! We make a pretty solid team, don't we, {PLAYER}?"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_slow_up))
    waitmovement
    applymovement(LOCALID_RIVAL, moves(face_down, emote_question_mark))
    waitmovement

    msgbox(format(
        "...Eh? Something was off about the Pokémon? What do you mean? \p"
        "Red eyes? A wicked dark aura? Hmmmn... I didn't see any of that.\p"
        "Well, either way, what's important is that Mareep is safe! Let's get him back to old man Paulie!"
    ), MSGBOX_AUTOCLOSE)

    fadescreenswapbuffers(FADE_TO_BLACK)
    teleportcamera(11, 8)
    addobject(OBJ_EVENT_ID_PLAYER)
    addobject(LOCALID_RIVAL)
    addobject(LOCALID_OLD_MAN)
    addobject(LOCALID_MAREEP)
    applymovement(LOCALID_OLD_MAN, moves(face_down))
    applymovement(LOCALID_RIVAL, moves(face_up))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    setobjectxy(OBJ_EVENT_ID_PLAYER, 11, 8)
    setobjectxy(LOCALID_RIVAL, 10, 8)
    setobjectxy(LOCALID_OLD_MAN, 11, 6)
    setobjectxy(LOCALID_MAREEP, 10, 6)
    fadescreenswapbuffers(FADE_FROM_BLACK)

    msgbox(format(
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}Oh, my precious baby is safe and sound! I am in your debt, trainers! \p"
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Heheh. It was nothing, really! That big Mightyena didn't stand a chance!\p"
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}Please, take one of these as a token of my gratitude!"
    ), MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_OLD_MAN, moves(walk_down))
    waitmovement
    giveitem(ITEM_LUCKY_EGG)
    closemessage
    applymovement(LOCALID_OLD_MAN, moves(walk_left, face_down, delay_16 * 3, walk_right, walk_up, face_down))
    waitmovement
    msgbox(format("{COLOR GREEN}Paul: {COLOR DARK_GRAY}Feel free to visit our farm anytime!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_OLD_MAN, moves(walk_up * 4))
    waitmovement
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    applymovement(LOCALID_MAREEP, moves(jump_in_place_down * 3))
    waitmovement
    waitmoncry
    applymovement(LOCALID_MAREEP, moves(walk_up * 4))
    waitmovement
    applymovement(LOCALID_RIVAL, moves(face_player))
    waitmovement
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Watching how in sync you and your Pokémon are, despite having just met them...\p"
        "{PLAYER}!{PAUSE 30} You should enter the Silverwing Festival Tournament!"
    ), MSGBOX_AUTOCLOSE)

    applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_question_mark, delay_16 * 4))
    waitmovement
    playse(SE_PIN)
	applymovement(LOCALID_RIVAL, moves(emote_exclamation_mark, walk_in_place_faster_right, delay_16 * 4)) 
    waitmovement

    // NOTE(poe): We had to split this message up, because originally it was too big for the text buffer

    msgbox(format(
        "Eeeh!?{PAUSE 60} You don't know about the tournament!?\p"
        "It's a tournament hosted during the Silverwing Festival where only the best trainers of Marya assemble to compete for the Silverwing Cup!\p"
        "I'm taking part in it, and I think you should to! As a natural trainer, you'd be a great competitor!\p"
        "But in order to qualify...{PAUSE 30} you'll have to complete the Marya Gym Challenge!"
    ))

    msgbox(format(
        "You'll have to travel across the three main islands of Oranna, Lumine, and Minerva... \p"
        "...and defeat their respective gym leaders in battle! \p"
        "Ooh... I can't wait! I wish the festival was tomorrow!\p"
        "Oh, you're right.{PAUSE 30} Neither of us would qualify if it started tomorrow...\p"
        "There I go being silly again, ehe."
    ))

    msgbox(format(
        "Well, I'm going to go back to the professor to report my findings.\p"
        "But {PLAYER}, you should go west of Maplegrove and head up to Tidalcove! \p"
        "That's where you'll find the first gym leader: Ruka!\p"
        "Ruka is really famous around here. On top of being a gym leader, she used to be a professional swimmer and swimsuit model!"
    ))

    msgbox(format(
        "With that said, bye-bye for now, {PLAYER}!\p"
        "Oh! I almost forgot! Let's exchange phone numbers so we can keep in touch!\p"
        "Mhm...{PAUSE 30} Alright...{PAUSE 30} and there we go! All set!\p"
        "Let's meet up again soon!"
    ), MSGBOX_AUTOCLOSE)

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(LOCALID_RIVAL, moves(walk_down * 5, walk_right * 6, walk_down * 5, delay_8 * 6))
    waitmovement
    setobjecthidden(LOCALID_RIVAL)
    setobjecthidden(LOCALID_OLD_MAN)
    setobjecthidden(LOCALID_MAREEP)
    special(HealPlayerParty)
    completequest(QUEST_MINTY_MEADOWS_MELISSA)
    startquest(QUEST_MARYA_GYM_CHALLENGE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Interact_Drifloon {
    faceplayer
    playmoncry(SPECIES_DRIFLOON, CRY_MODE_ENCOUNTER)
    waitmoncry
    setflag(FLAG_SMART_WILD_AI)
	setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    setwildbattle(SPECIES_DRIFLOON, 13)
    dowildbattle
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    clearflag(FLAG_SMART_WILD_AI)
    specialvar(VAR_RESULT, GetBattleOutcome)

    if (var(VAR_RESULT) == B_OUTCOME_WON || var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
        setflag(FLAG_DAILY_MINTY_MEADOWS_DRIFLOON)
    }
}

script ISLANDGAME_MINTY_MEADOWS_2_Interact_FlowerSign {
    msgbox(format(
        "MINTY MEADOWS\n"
        "Beware of scary Drifloon at night!"
    ), MSGBOX_SIGN)
}

script ISLANDGAME_MINTY_MEADOWS_2_Interact_TunnelSign {
    msgbox(format(
        "TRAVELER'S TUNNEL\n"
        "WARNING: THICK FOG INSIDE!"
    ), MSGBOX_SIGN)
}

script ISLANDGAME_MINTY_MEADOWS_2_Interact_PopsHouse {
    msgbox(format(
        "POPS' RESIDENCE\n"
        "Owner of Minty Meadows!"
    ), MSGBOX_SIGN)
}

// section: triggers

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_RivalFight_Right2 {
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 2, face_up))
    waitmovement
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_RivalFight_Right1 {
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right, face_up))
    waitmovement
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_RivalFight {
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_RivalFight_Left1 {
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left, face_up))
    waitmovement
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_RivalFight_Left2 {
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 2, face_up))
    waitmovement
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_RivalFight_Left3 {
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 3, face_up))
    waitmovement
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_Forest {
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
    case MINTY_MEADOWS_BATTLING_RIVAL:
        playse(SE_PIN)
        applymovement(LOCALID_OLD_MAN, moves(emote_exclamation_mark, delay_8 * 6, face_player)) 
        waitmovement
        msgbox(format("Ah, young trainer! Please give me a moment. I am trying to count my Mareep."), MSGBOX_AUTOCLOSE)
        applymovement(LOCALID_OLD_MAN, moves(face_original_direction))
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left))
        waitmovement
    case MINTY_MEADOWS_SEARCHING_FOR_MAREEP:
    case MINTY_MEADOWS_BATTLING_MIGHTYENA:
        if (checkfollowernpc) {
            facefollowernpc

            message(format(
                "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Eh? We can't leave yet! We told old man Paulie we'd help find his Mareep!\p"
                "Do you really have to leave?"
            ))

            waitmessage
            dynmultipush("Yes", YES)
            dynmultipush("No", NO)

            switch (dynmultistack(20, 8, TRUE, 3, FALSE, NO, DYN_MULTICHOICE_CB_NONE)) {
            case YES:
                msgbox(format("Okay... Please come back as soon as you can!"), MSGBOX_AUTOCLOSE)
                unsetfollowernpc(LOCALID_RIVAL)
                applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left))
                applymovement(LOCALID_RIVAL, moves(walk_right))
                waitmovement
            case NO:
                msgbox(format("Alright! Let's get back to business!"), MSGBOX_AUTOCLOSE)
                applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 2))
                waitmovement
            }
        }
        else {
            applymovement(LOCALID_RIVAL, moves(walk_in_place_left))
            waitmovement
            playse(SE_PIN)
            applymovement(LOCALID_RIVAL, moves(emote_exclamation_mark))
            msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Let's go get that Mareep!"), MSGBOX_AUTOCLOSE)
            applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 2))
            waitmovement
            setfollowernpc(LOCALID_RIVAL, FNPC_ALL, 0, PARTNER_MEADOWS_RIVAL)
        }
    }
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_SeeMareep {
    msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}We finally found it!"), MSGBOX_AUTOCLOSE)
    applymovement(LOCALID_MIGHTYENA, moves(walk_up))
    applymovement(LOCALID_MAREEP, moves(emote_exclamation_mark))
    playmoncry(SPECIES_MIGHTYENA, CRY_MODE_ENCOUNTER)
    waitmoncry
    playmoncry(SPECIES_MAREEP, CRY_MODE_WEAK)
    waitmovement(LOCALID_MIGHTYENA)
    waitmoncry
    playse(SE_PIN)
	applymovement(LOCALID_RIVAL, moves(emote_exclamation_mark, delay_8 * 6, walk_in_place_faster_up)) 
	waitmovement
    msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Agh! Quick, {PLAYER}! We need to save that Mareep!"), MSGBOX_AUTOCLOSE)
    setvar(VAR_MINTY_MEADOWS_STATE, MINTY_MEADOWS_BATTLING_MIGHTYENA)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trigger_Cave {
    msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Huh? We have to help Mareep!"), MSGBOX_AUTOCLOSE)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
    waitmovement
}

// section: trainers

script ISLANDGAME_MINTY_MEADOWS_2_Trainer_UpperDouble_Rancher {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_11, format("Howdy pardner! Howsabout we get a quick scrap in?"), format("Well I'll be."))
	msgbox(format("A mighty fine party of Pokes ya got on ya."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trainer_UpperDouble_Cowgirl {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_12, format("My Pokemans are purty cute. Wanna take a gander?"), format("Ain't she just the sweetest lil thang you've eva laid yer eyes on?"))
	msgbox(format("I love my darlin' lil ball of cotton candy. Even if she don't fight too well."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trainer_LowerDouble_BugCatcher {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_13, format("I have a super special Bug Pokémon. You literally can't get it.\pHow jealous are you?"), format("Ack! My super special bug Pokémon!"))

	msgbox(format(
        "Okay, maybe I should be more specific.\p"
        "You can't catch a Vivillon with the same pattern as mine here in Marya.\p"
        "That's because a Vivillon's pattern is dependent on where it lives!"
    ), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trainer_LowerDouble_Lass {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_14, format("Hi!"), format("Heck!"))
	msgbox(format("I'm not a very interesting person, so I don't really say much."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trainer_FinalDouble_Maniac {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_15, format("POKÉMON IS LOVE! POKÉMON IS LIFE!"), format("It's all Kyogre now..."))
	msgbox(format("I was only nine years old when I met my Riolu. It's the coolest Pokémon ever."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Trainer_FinalDouble_Youngster {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_16, format("Shorts are awesome. They're so comfy to wear. Don't you agree?"), format("I think I'm going to need a new pair of shorts..."))
	msgbox(format("Shorts are easily an S tier pick for pants!"), MSGBOX_AUTOCLOSE)
}
