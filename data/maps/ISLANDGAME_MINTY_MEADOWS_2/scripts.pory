// possible values of VAR_MINTY_MEADOWS_STATE
const STATE_INITIAL_RIVAL = 0
const STATE_STARTED_QUEST = 1
const STATE_PAUSED_QUEST = 2
const STATE_PRE_FIGHT_SIGHTING = 3
const STATE_POST_FIGHT = 4

const ID_RIVAL = 8
const ID_OLD_MAN = 9
const ID_MAREEP = 6
const ID_MIGHTYENA = 7
const ID_MAREEP_EXTRA1 = 14
const ID_MAREEP_EXTRA2 = 15
const ID_MAREEP_EXTRA3 = 16
const ID_DRIFLOON = 17

// an offscreen position that is never visible
const HIDDEN_X = -3
const HIDDEN_Y = 1

const MELISSA_GFX = OBJ_EVENT_GFX_MELISSA

mapscripts ISLANDGAME_MINTY_MEADOWS_2_MapScripts {
    MAP_SCRIPT_ON_RESUME {
        specialvar(VAR_RESULT, GetBattleOutcome)
        if (flag(FLAG_SYS_CTRL_OBJ_DELETE) && var(VAR_RESULT) == B_OUTCOME_CAUGHT || var(VAR_RESULT) == B_OUTCOME_WON) {
            removeobject(VAR_LAST_TALKED)
        }
    }
    MAP_SCRIPT_ON_TRANSITION {
        // drifloon can only appear at night
        if (gettimeofday() != TIME_NIGHT) {
            setobjectxyperm(ID_DRIFLOON, HIDDEN_X, HIDDEN_Y) 
        }

        // allow player freely go to places that were restricted prior to beating shadow mightyena
        if (flag(FLAG_MINTY_MEADOWS_RIVAL_DEFEATED)) {
            setvar(VAR_TEMP_5, 1) 
        }

        switch (var(VAR_MINTY_MEADOWS_STATE)) {
            case STATE_INITIAL_RIVAL:       call(ISLANDGAME_MINTY_MEADOWS_2_InitialRival)
            case STATE_STARTED_QUEST:       call(ISLANDGAME_MINTY_MEADOWS_2_StartedQuest)
            case STATE_PAUSED_QUEST:        call(ISLANDGAME_MINTY_MEADOWS_2_PausedQuest)
            case STATE_PRE_FIGHT_SIGHTING:  call(ISLANDGAME_MINTY_MEADOWS_2_SawMareepPreFight)
            case STATE_POST_FIGHT:          call(ISLANDGAME_MINTY_MEADOWS_2_DefeatedMightyena)
        }
    }
}

// section: object positions

// The following scripts set up the object positions depending on the state
// It gets around needing a bunch of hide flags by just moving things offscreen when unneeded

// Sets up the Rival fight and old man blocking the side
script ISLANDGAME_MINTY_MEADOWS_2_InitialRival {
    setobjectxyperm(ID_RIVAL, 12, 5)
    setobjectxyperm(ID_OLD_MAN, 32, 10)
    setobjectxyperm(ID_MAREEP_EXTRA1, 35, 8)
    setobjectxyperm(ID_MAREEP_EXTRA2, 34, 10)
    setobjectxyperm(ID_MAREEP_EXTRA3, 33, 12)
    setobjectxyperm(ID_MAREEP, HIDDEN_X, HIDDEN_Y)
    setobjectxyperm(ID_MIGHTYENA, HIDDEN_X, HIDDEN_Y)
    return
}

// After beating the rival, sets up the mareep sighting on the side
script ISLANDGAME_MINTY_MEADOWS_2_StartedQuest {
    // TODO(poe): code smell w/ this use of VAR_TEMP_4
    setvar(VAR_TEMP_4, 1)

    setobjectxyperm(ID_OLD_MAN, 10, 9)
    setobjectxyperm(ID_MAREEP, 61, 13)
    setobjectxyperm(ID_MIGHTYENA, 61, 15)
    setobjectmovementtype(ID_MAREEP, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype(ID_MIGHTYENA, MOVEMENT_TYPE_FACE_UP)
    return
}

// Come back to the quest when entering again
script ISLANDGAME_MINTY_MEADOWS_2_PausedQuest {
    setvar(VAR_TEMP_4, 1)
    setobjectxyperm(ID_RIVAL, 34, 10)
    setobjectmovementtype(ID_RIVAL, MOVEMENT_TYPE_LOOK_AROUND)
    setobjectxyperm(ID_OLD_MAN, 10, 9)
    setobjectxyperm(ID_MAREEP, 61, 13)
    setobjectxyperm(ID_MIGHTYENA, 61, 15)
    setobjectmovementtype(ID_MAREEP, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype(ID_MIGHTYENA, MOVEMENT_TYPE_FACE_UP)
    return
}

// After watching the pre-fight cutscene, wait for the player to beat the mightyena
script ISLANDGAME_MINTY_MEADOWS_2_SawMareepPreFight {
    setvar(VAR_TEMP_4, 1)
    setobjectxyperm(ID_OLD_MAN, 10, 9)
    setobjectxyperm(ID_MAREEP, 61, 13)
    setobjectxyperm(ID_MIGHTYENA, 61, 14)
    setobjectmovementtype(ID_MAREEP, MOVEMENT_TYPE_FACE_DOWN)
    setobjectmovementtype(ID_MIGHTYENA, MOVEMENT_TYPE_FACE_UP)
    return
}

// After the player beat the mightyena, remove all mareep-quest related stuff
script ISLANDGAME_MINTY_MEADOWS_2_DefeatedMightyena {
    setvar(VAR_TEMP_4, 0)
    // setobjectxyperm(ID_RIVAL, HIDDEN_X, HIDDEN_Y)
    // setobjectxyperm(ID_OLD_MAN, 11, 3)
    // setobjectxyperm(ID_MAREEP, HIDDEN_X, HIDDEN_Y)
    setobjectxyperm(ID_MIGHTYENA, HIDDEN_X, HIDDEN_Y)
    return
}

// section: interactions

script ISLANDGAME_MINTY_MEADOWS_2_OldMan_Interact {
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
        case STATE_INITIAL_RIVAL:            
        case STATE_STARTED_QUEST:            msgbox(format("I could've sworn it was somewhere in the forest to the east..."), MSGBOX_NPC)
        case STATE_PAUSED_QUEST:             msgbox(format("I could've sworn it was somewhere in the forest to the east..."), MSGBOX_NPC)
        case STATE_PRE_FIGHT_SIGHTING:       msgbox(format("What? A wild Mightyena was chasing my precious Mareep?\pYou have to save him, please!"), MSGBOX_NPC)
        case STATE_POST_FIGHT:               msgbox(format("Thank you so much for saving my precious Mareep!"), MSGBOX_NPC)
    }
}

script ISLANDGAME_MINTY_MEADOWS_2_Rival_Interact {
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
        case STATE_INITIAL_RIVAL:            
        case STATE_STARTED_QUEST:            msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Hmm... where could it be?"), MSGBOX_NPC)
        case STATE_PRE_FIGHT_SIGHTING:       msgbox(format("We need to save that Mareep!"), MSGBOX_NPC)
        case STATE_POST_FIGHT:               msgbox(format("Phew... I need to take a break after all that running..."), MSGBOX_NPC)
        default:
    }
    // msgbox(format("{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Oh! Are your Pokémon hurt, {PLAYER}? Let me patch them up!"), MSGBOX_AUTOCLOSE)
    // playfanfare(MUS_HEAL)
    // waitfanfare()
    // special(HealPlayerParty)
    // msgbox(format("There we go! They're all fixed up!"))
    closemessage
    release
}

script ISLANDGAME_MINTY_MEADOWS_2_FlowerSign_Interact {
    msgbox(format("{COLOR DARK_GRAY}MINTY MEADOWS\nBeware of scary Drifloon at night!"), MSGBOX_SIGN)
}

script ISLANDGAME_MINTY_MEADOWS_2_TunnelSign_Interact {
    msgbox(format("{COLOR DARK_GRAY}TRAVELER'S TUNNEL\nWARNING: THICK FOG INSIDE!"), MSGBOX_SIGN)
}

script ISLANDGAME_MINTY_MEADOWS_2_PopsHouse_Interact {
    msgbox(format("{COLOR DARK_GRAY}POPS' RESIDENCE\nOwner of Minty Meadows!"), MSGBOX_SIGN)
}


// section: rival fight

script ISLANDGAME_MINTY_MEADOWS_2_OldMan_PreRival {
    lock
    playse(SE_PIN)
	applymovement(ID_OLD_MAN, moves(emote_exclamation_mark, delay_8 * 6, face_player)) 
    waitmovement(0)
    msgbox(format("{COLOR DARK_GRAY}Ah, young trainer! Please give me a moment. I am trying to count my Mareep."))
    closemessage
    applymovement(ID_OLD_MAN, moves(face_original_direction))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left, face_left))
    waitmovement(0)
    release
}

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Trigger1 {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 2, face_up))
    waitmovement(0)
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Main)
}

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Trigger2 {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right, face_up))
    waitmovement(0)
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Main)
}

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Trigger3 {
    lock
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Main)
}

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Trigger4 {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left, face_up))
    waitmovement(0)
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Main)
}

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Trigger5 {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 2, face_up))
    waitmovement(0)
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Main)
}

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Trigger6 {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_left * 3, face_up))
    waitmovement(0)
    goto(ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Main)
}

// TODO(poe): Probably not needed...
script ISLANDGAME_MINTY_MEADOWS_2_TeleportGauntlet {
    fadescreenswapbuffers(FADE_TO_BLACK)
    removeobject(ID_RIVAL)
    setobjectxyperm(ID_RIVAL, HIDDEN_X, HIDDEN_Y)

    setvar(VAR_0x8000, 34) // 34, 11 is the coordinates for being inside the maze
    setvar(VAR_0x8001, 11)
    callnative(TeleportCamera) 
    addobject(OBJ_EVENT_ID_PLAYER)

    setobjectxy(OBJ_EVENT_ID_PLAYER, 34, 11)

    // addobject(ID_RIVAL)
    // setobjectxyperm(ID_RIVAL, 34, 10)
    // setobjectxy(ID_RIVAL, 34, 11)
    fadescreenswapbuffers(FADE_FROM_BLACK)

    // create follower
    createfollowernpc(OBJ_EVENT_GFX_MELISSA, FNPC_ALL, ISLANDGAME_MINTY_MEADOWS_2_Rival_Interact, PARTNER_MEADOWS_RIVAL)
    // setfollowernpc(ID_RIVAL, FNPC_ALL, ISLANDGAME_MINTY_MEADOWS_2_Rival_Interact, PARTNER_MEADOWS_RIVAL)

    return
}

script ISLANDGAME_MINTY_MEADOWS_2_RivalFight_Main {
    setflag(FLAG_FOLLOWERS_DISABLED)

    applymovement(ID_RIVAL, moves(
        face_up,
        delay_16, delay_4,
        face_right,
        delay_16, delay_8,
        face_left,
        delay_16, delay_8,
        face_down,
        delay_16,
        face_right,
        delay_16, delay_8,
        face_up,
        delay_16,
        step_end,
        walk_in_place_faster_up,
        delay_16 * 2
    ))
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Nnngh...{PAUSE 30} I'm only missing one more Pokémon!{PAUSE 30} Where in the world is it?\p"
        "{COLOR DARK_GRAY}Waah! I give up! I'm convinced it doesn't even exist! \p"
        "{COLOR DARK_GRAY}Eh?"
    ))
    closemessage

    applymovement(ID_RIVAL, moves(emote_question_mark, face_down, delay_8 * 6))
	waitmovement(0)
	playbgm(MUS_HG_LYRA,TRUE)
    applymovement(ID_RIVAL, moves(walk_left * 2, walk_down * 2))
    waitmovement(0)

    msgbox(format(
        "Hiya there!{PAUSE 30}... Err...{PAUSE 30} umm,{PAUSE 30} have we met before?"
    ))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_up, delay_16 * 2))
    waitmovement(0)
    playse(SE_PIN)
	applymovement(ID_RIVAL, moves(emote_exclamation_mark, delay_8 * 6)) 
	waitmovement(0)

    msgbox(format(
        "Ooooh! You're {PLAYER}? The professor told me about you earlier today!\p"
        "It's good to meet you! I'm Melissa, a rising Pokémon trainer just like you!\p"
        "I was trying to fill out my Pokédex here, but I couldn't find a Scorbunny, no matter how hard I looked...\p"
        "Hope the isles are treating you kindly so far! I've been here all my life, so if ya ever need guidance, leave it to me!\p"
        "Oh!{PAUSE 30} Now that you're here, let's have a Pokémon battle! I need some action!\p"
        "I might be new just like you... but don't you underestimate me!"
    ))
    closemessage

    trainerbattle_no_intro(TRAINER_MINTY_MEADOWS_RIVAL, "Ack! Looks like I was the one doing the\nunderestimating!")
    if (specialvar(VAR_RESULT, GetBattleOutcome) != B_OUTCOME_WON) {
        return
    }

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Drats, I wasn't expecting to lose to you, {PLAYER}... \p"
        "{COLOR DARK_GRAY}W-Wait, I didn't mean it like that! I was just surprised at how skilled you are!\p"
        "{COLOR DARK_GRAY}Sorry... I say things before I speak a lot, ehe..."
    ))
    closemessage

	savebgm(MUS_DUMMY)
	fadedefaultbgm

    // old man walks over and starts quest
    addobject(ID_OLD_MAN)
    setobjectxyperm(ID_MAREEP_EXTRA1, HIDDEN_X, HIDDEN_Y)
    setobjectxyperm(ID_MAREEP_EXTRA2, HIDDEN_X, HIDDEN_Y)
    setobjectxyperm(ID_MAREEP_EXTRA3, HIDDEN_X, HIDDEN_Y)
    setobjectxy(ID_OLD_MAN, 18, 12)

    msgbox(format(
        "{COLOR DARK_GRAY}Excuse me! Young trainers!"
    ))
    closemessage

    applymovement(ID_OLD_MAN, moves(walk_left * 8, walk_up * 3))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Farm Owner: {COLOR DARK_GRAY}I'm Paul, I'm one of the owners of Fresa Farms.\p"
        "{COLOR DARK_GRAY}Have you two seen a Mareep with a blue collar wandering around by chance? \p"
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}I haven't... Did you lose one?\p"
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}I'm afraid so... When I was counting my Mareep after taking them out to graze, I noticed one was missing."
    ))
    closemessage

    playse(SE_PIN)
	applymovement(ID_RIVAL, moves(emote_exclamation_mark, delay_8 * 6)) 
	waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Oh my... that's terrible! We'll help you look, right {PLAYER}?"
    ))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up, delay_16 * 2, walk_in_place_faster_up, delay_16 * 2, face_down, delay_16 * 2))
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}Really? Oh, I can't thank you two enough! \p"
        "{COLOR DARK_GRAY}The last place I saw my precious Mareep was in the forest to the east."
    ))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Sounds good! {PLAYER}! Let's head over there immediately!"
        ))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    waitmovement(0)

    special(HealPlayerParty)
    call(ISLANDGAME_MINTY_MEADOWS_2_TeleportGauntlet)
    setvar(VAR_MINTY_MEADOWS_STATE, STATE_STARTED_QUEST)
    call(ISLANDGAME_MINTY_MEADOWS_2_StartedQuest)
    release
}

// section: pre-fight cutscene

script ISLANDGAME_MINTY_MEADOWS_2_SeeMareep_2 {
    lock

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}We finally found it!"
    ))
    closemessage

    applymovement(ID_MIGHTYENA, moves(walk_up, step_end))
    applymovement(ID_MAREEP, moves(emote_exclamation_mark))
    playmoncry(SPECIES_MIGHTYENA, CRY_MODE_ENCOUNTER)
    waitmoncry
    playmoncry(SPECIES_MAREEP, CRY_MODE_WEAK)
    waitmovement(ID_MIGHTYENA)
    waitmoncry

    playse(SE_PIN)
	applymovement(ID_RIVAL, moves(emote_exclamation_mark, delay_8 * 6, walk_in_place_faster_up)) 
	waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Agh! Quick, {PLAYER}! We need to save that Mareep!"
    ))
    closemessage

    setvar(VAR_MINTY_MEADOWS_STATE, STATE_PRE_FIGHT_SIGHTING)
    call(ISLANDGAME_MINTY_MEADOWS_2_SawMareepPreFight)
    release
}

// section: fight

script ISLANDGAME_MINTY_MEADOWS_2_BattleMightyena {
    lock 
    faceplayer
    setflag(FLAG_SMART_WILD_AI)
    setflag(FLAG_NO_CATCHING)
    setflag(FLAG_NO_RUNNING)
    createmon(B_SIDE_OPPONENT, B_FLANK_LEFT, SPECIES_SHADOW_MIGHTYENA, 25, ITEM_SITRUS_BERRY, NUM_NATURES, 0, MON_GENDERLESS, 252, 252, 252, 252, 252, 252, 31, 31, 31, 31, 31, 31, MOVE_SNARL, MOVE_POISON_FANG, MOVE_SAND_ATTACK, MOVE_SWAGGER)
    setwilddoubleflag
    playmoncry(SPECIES_MIGHTYENA, CRY_MODE_ENCOUNTER)
    waitmoncry
    dowildbattle
    clearflag(FLAG_SMART_WILD_AI)
    clearflag(FLAG_NO_CATCHING)
    clearflag(FLAG_NO_RUNNING)

    if (specialvar(VAR_RESULT, GetBattleOutcome) == B_OUTCOME_RAN) {
        release
        end
    }

    destroyfollowernpc

    if (specialvar(VAR_RESULT, GetBattleOutcome) == B_OUTCOME_LOST) {
        release
        end
    }

    setvar(VAR_MINTY_MEADOWS_STATE, STATE_POST_FIGHT)
    call(ISLANDGAME_MINTY_MEADOWS_2_DefeatedMightyena)
    setvar(VAR_0x8000, 61)
    setvar(VAR_0x8001, 16)
    callnative(TeleportCamera) // this is some magic, shoutout to the pret discord (and see src/islandgame_scrcmd.c)
    addobject(ID_RIVAL)
    applymovement(ID_RIVAL, moves(face_up))
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    setobjectxy(OBJ_EVENT_ID_PLAYER, 61, 16)
    setobjectxy(ID_RIVAL, 60, 16)
    fadescreenswapbuffers(FADE_TO_BLACK)
    removeobject(ID_MIGHTYENA) 
    setobjectxyperm(ID_MIGHTYENA, HIDDEN_X, HIDDEN_Y)
    fadescreenswapbuffers(FADE_FROM_BLACK)

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Phew... That was an intense fight. Thank goodness we came in time!\p"
        "{COLOR RED}(That Mightyena...{PAUSE 30} Something seemed off about it.)\p"
        "{COLOR RED}(It had some sort of dark aura... radiating from it?)"
    ))
    closemessage

    applymovement(ID_RIVAL, moves(walk_up * 3, face_right))
    waitmovement(0)

    msgbox(format(
        "{COLOR DARK_GRAY}It looks like Mareep isn't hurt at all. That's a relief..."
    ))
    closemessage

    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    applymovement(ID_MAREEP, moves(face_left, jump_in_place_left * 3))
    waitmovement(0)

    msgbox(format(
        "{COLOR DARK_GRAY}Reeeep... \p"
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Awh. It's okay. You're safe with us now!"
    ))

    applymovement(ID_RIVAL, moves(face_down))
    waitmovement(0)

    msgbox(format(
        "{COLOR DARK_GRAY}Ehe...! We make a pretty solid team, don't we, {PLAYER}?"
    ))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_slow_up))
    waitmovement(0)
    applymovement(ID_RIVAL, moves(face_down, emote_question_mark))
    waitmovement(0)

    msgbox(format(
        "{COLOR DARK_GRAY}...Eh? Something was off about the Pokémon? What do you mean? \p"
        "{COLOR DARK_GRAY}Red eyes? A wicked dark aura? Hmmmn... I didn't see any of that."
    ))
    closemessage

    applymovement(ID_RIVAL, moves(face_right))
    waitmovement(0)

    msgbox(format(
        "Well, either way, what's important is that Mareep is safe! Let's get him back to old man Paulie!"
    ))
    closemessage

    fadescreenswapbuffers(FADE_TO_BLACK)
    setvar(VAR_0x8000, 11)
    setvar(VAR_0x8001, 8)
    callnative(TeleportCamera) // this is some magic, shoutout to the pret discord (and see src/islandgame_scrcmd.c)
    addobject(OBJ_EVENT_ID_PLAYER)
    addobject(ID_RIVAL)
    addobject(ID_OLD_MAN)
    addobject(ID_MAREEP)
    applymovement(ID_OLD_MAN, moves(face_down))
    waitmovement(0)
    applymovement(ID_RIVAL, moves(face_up))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    waitmovement(0)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 11, 8)
    setobjectxy(ID_RIVAL, 10, 8)
    setobjectxy(ID_OLD_MAN, 11, 6)
    setobjectxy(ID_MAREEP, 10, 6)
    fadescreenswapbuffers(FADE_FROM_BLACK)

    msgbox(format(
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}Oh, my precious baby is safe and sound! I am in your debt, trainers! \p"
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Heheh. It was nothing, really! That big Mightyena didn't stand a chance!\p"
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}Please, take one of these as a token of my gratitude!"
    ))
    closemessage

    applymovement(ID_OLD_MAN, moves(walk_down))
    waitmovement(0)
    giveitem(ITEM_LUCKY_EGG)
    closemessage
    applymovement(ID_OLD_MAN, moves(walk_left, face_down, delay_16 * 3, walk_right, walk_up, face_down))
    waitmovement(0)

    msgbox(format(
        "{COLOR GREEN}Paul: {COLOR DARK_GRAY}Feel free to visit our farm anytime!"
    ))
    closemessage

    applymovement(ID_OLD_MAN, moves(walk_up * 4))
    waitmovement(0)
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    applymovement(ID_MAREEP, moves(jump_in_place_down * 3))
    waitmovement(0)
    waitmoncry
    applymovement(ID_MAREEP, moves(walk_up * 4))
    waitmovement(0)
    applymovement(ID_RIVAL, moves(face_player))
    waitmovement(0)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_left))

    msgbox(format(
        "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Watching how in sync you and your Pokémon are, despite having just met them...\p"
        "{COLOR DARK_GRAY}{PLAYER}!{PAUSE 30} You should enter the Silverwing Festival Tournament!"
    ))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_question_mark, delay_16 * 4))
    waitmovement(0)
    playse(SE_PIN)
	applymovement(ID_RIVAL, moves(emote_exclamation_mark, walk_in_place_faster_right, delay_16 * 4)) 
    waitmovement(0)

    msgbox(format(
        "{COLOR DARK_GRAY}Eeeh!?{PAUSE 60} You don't know about the tournament!?\p"
        "{COLOR DARK_GRAY}It's a tournament hosted during the Silverwing Festival where only the best trainers of Marya assemble to compete for the Silverwing Cup!\p"
        "{COLOR DARK_GRAY}I'm taking part in it, and I think you should to! As a natural trainer, you'd be a great competitor!\p"
        "{COLOR DARK_GRAY}But in order to qualify...{PAUSE 30} you'll have to complete the Marya Gym Challenge!"
    ))
    msgbox(format(
        "{COLOR DARK_GRAY}You'll have to travel across the three main islands of Oranna, Lumine, and Minerva... \p"
        "{COLOR DARK_GRAY}...and defeat their respective gym leaders in battle! \p"
        "{COLOR DARK_GRAY}Ooh... I can't wait! I wish the festival was tomorrow!\p"
        "{COLOR DARK_GRAY}Oh, you're right.{PAUSE 30} Neither of us would qualify if it started tomorrow...\p"
        "There I go being silly again, ehe."
    ))
    msgbox(format(
        "{COLOR DARK_GRAY}Well, I'm going to go back to the professor to report my findings.\p"
        "{COLOR DARK_GRAY}But {PLAYER}, you should go west of Maplegrove and head up to Tidalcove! \p"
        "{COLOR DARK_GRAY}That's where you'll find the first gym leader: Ruka!\p"
        "{COLOR DARK_GRAY}Ruka is really famous around here. On top of being a gym leader, she used to be a professional swimmer and swimsuit model!"
    ))
    msgbox(format(
        "{COLOR DARK_GRAY}With that said, bye-bye for now, {PLAYER}!\p"
        "{COLOR DARK_GRAY}Oh! I almost forgot! Let's exchange phone numbers so we can keep in touch!\p"
        "{COLOR DARK_GRAY}Mhm...{PAUSE 30} Alright...{PAUSE 30} and there we go! All set!\p"
        "{COLOR DARK_GRAY}Let's meet up again soon!"
    ))
    closemessage

    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
    applymovement(ID_RIVAL, moves(walk_down * 5, walk_right * 6, walk_down * 5))
    applymovement(ID_RIVAL, moves(delay_8 * 6))
    waitmovement(0)

    removeobject(ID_RIVAL)
    removeobject(ID_MAREEP)
    removeobject(ID_OLD_MAN)
    setobjectxyperm(ID_RIVAL, HIDDEN_X, HIDDEN_Y)
    setobjectxyperm(ID_OLD_MAN, HIDDEN_X, HIDDEN_Y)
    setobjectxyperm(ID_MAREEP, HIDDEN_X, HIDDEN_Y)
    special(HealPlayerParty)
    completequest(QUEST_MINTY_MEADOWS_MELISSA)
    startquest(QUEST_MARYA_GYM_CHALLENGE)
    clearflag(FLAG_FOLLOWERS_DISABLED)
    setflag(FLAG_MINTY_MEADOWS_RIVAL_DEFEATED)
    setvar(VAR_TEMP_5, 1) // let player go to places that were restricted prior
    release
}

script ISLANDGAME_MINTY_MEADOWS_2_Mareep {
    lock
    playmoncry(SPECIES_MAREEP, CRY_MODE_WEAK)
    msgbox(format(
        "{COLOR DARK_GRAY}Reep! Reep! {COLOR BLUE}(BRO HELP ME PLZ)"
    ))
    release
}

script ISLANDGAME_MINTY_MEADOWS_2_CantLeave {
    lock
    switch (var(VAR_MINTY_MEADOWS_STATE)) {
        case STATE_PAUSED_QUEST:
            applymovement(ID_RIVAL, moves(walk_in_place_left))
            waitmovement(0)
            playse(SE_PIN)
            applymovement(ID_RIVAL, moves(emote_exclamation_mark))

            msgbox(format(
                "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}You're back! Let's go get that Mareep!"
            ))
            closemessage

            // applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
            // createfollowernpc(OBJ_EVENT_GFX_MELISSA, FNPC_ALL, ISLANDGAME_MINTY_MEADOWS_2_Rival_Interact, PARTNER_MEADOWS_RIVAL)
            // setfollowernpc(ID_RIVAL, FNPC_ALL, ISLANDGAME_MINTY_MEADOWS_2_Rival_Interact, PARTNER_MEADOWS_RIVAL)
            setvar(VAR_MINTY_MEADOWS_STATE, STATE_STARTED_QUEST)
            call(ISLANDGAME_MINTY_MEADOWS_2_TeleportGauntlet)

        case STATE_STARTED_QUEST:
        case STATE_PRE_FIGHT_SIGHTING:
            facefollowernpc
            msgbox(format(
                "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Eh? We can't leave yet! We told old man Paulie we'd help find his Mareep!\p"
                "Do you really have to leave?"
            ))
            dynmultipush("Yes", YES)
            dynmultipush("No", NO)
            switch (dynmultistack(20, 8, TRUE, 3, FALSE, NO, DYN_MULTICHOICE_CB_NONE)) {
                case YES:
                    setvar(VAR_MINTY_MEADOWS_STATE, STATE_PAUSED_QUEST) 
                    
                    msgbox(format(
                        "Okay... Please come back as soon as you can!"
                    ))
                    closemessage

                    fadescreenswapbuffers(FADE_TO_BLACK)
                    destroyfollowernpc()
                    // addobject(ID_RIVAL)
                    setobjectxyperm(ID_RIVAL, 34, 10)
                    setvar(VAR_0x8000, 30) 
                    setvar(VAR_0x8001, 10)
                    callnative(TeleportCamera) 
                    addobject(OBJ_EVENT_ID_PLAYER)
                    setobjectxy(OBJ_EVENT_ID_PLAYER, 30, 10)
                    fadescreenswapbuffers(FADE_FROM_BLACK)
                    // applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
                    // waitmovement(OBJ_EVENT_ID_PLAYER)
                    // applymovement(ID_RIVAL, Common_Movement_FaceLeft)
                    // applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
                    // waitmovement(OBJ_EVENT_ID_PLAYER)
                    // waitmovement(ID_RIVAL)
                    // waitmessage
                    // waitbuttonpress
                    // closemessage
                case NO:
                    msgbox(format(
                        "Alright! Let's get back to business!"
                    ))
                    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right))
                    waitmovement(0)
                    closemessage
            }
    }
    release
}

script ISLANDGAME_MINTY_MEADOWS_2_CantLeave2 {
    if(!flag(FLAG_MINTY_MEADOWS_RIVAL_DEFEATED)) {
        msgbox(format(
            "{COLOR GREEN}Melissa: {COLOR DARK_GRAY}Huh? We have to help Mareep!"
        ))
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
        waitmovement(0)
        closemessage
    }
}

script ISLANDGAME_MINTY_MEADOWS_2_DrifloonBattle {
    lock 
    faceplayer
    playmoncry(SPECIES_DRIFLOON, CRY_MODE_ENCOUNTER)
    waitmoncry
    setflag(FLAG_SMART_WILD_AI)
	setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    setwildbattle(SPECIES_DRIFLOON, 13)
    dowildbattle
	clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
    clearflag(FLAG_SMART_WILD_AI)

    specialvar(VAR_RESULT, GetBattleOutcome)
    if (var(VAR_RESULT) == B_OUTCOME_WON || var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
        setflag(FLAG_DAILY_MINTY_MEADOWS_DRIFLOON)
    }
    if (var(VAR_RESULT) == B_OUTCOME_WON) {
        goto(Common_EventScript_RemoveStaticPokemon)
    }

    release
}

script ISLANDGAME_MINTY_MEADOWS_2_CantGoYet {
    if(!flag(FLAG_MINTY_MEADOWS_RIVAL_DEFEATED)) {
        msgbox(format("{COLOR RED}(I should really help the farm owner find his Mareep first...)"))
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_down))
        waitmovement(0)
        closemessage
    }
}

///TRAINERS

script ISLANDGAME_MINTY_MEADOWS_2_Rival_Trainer3 {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_11, format("{COLOR DARK_GRAY}Howdy pardner! Howsabout we get a quick scrap in?"), "Well I'll be.")
	msgbox(format("A mighty fine party of Pokes ya got on ya."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Rival_Trainer4 {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_12, format("{COLOR DARK_GRAY}My Pokemans are purty cute. Wanna take a gander?"), "Ain't she just the sweetest lil thang\n you've eva laid yer eyes on?")
	msgbox(format("I love my darlin' lil ball of cotton candy. Even if she don't fight too well."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Rival_Trainer5 {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_13, format("{COLOR DARK_GRAY}I have a super special Bug Pokémon. You literally can't get it.\pHow jealous are you?"), "Ack! My super special bug Pokémon!")
	msgbox(format("Okay, maybe I should be more specific.\pYou can't catch a Vivillon with the same pattern as mine here in Marya.\pThat's because a Vivillon's pattern is dependent on where it lives!"), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Rival_Trainer6 {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_14, format("{COLOR DARK_GRAY}Hi!"), "Heck!")
	msgbox(format("I'm not a very interesting person, so I don't really say much."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Rival_Trainer7 {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_15, format("{COLOR DARK_GRAY}POKÉMON IS LOVE! POKÉMON IS LIFE!"), "It's all Kyogre now...")
	msgbox(format("I was only nine years old when I met my Riolu. It's the coolest Pokémon ever."), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MINTY_MEADOWS_2_Rival_Trainer8 {
    trainerbattle_single(TRAINER_MINTY_MEADOWS_16, format("{COLOR DARK_GRAY}Shorts are awesome. They're so comfy to wear. Don't you agree?"), "I think I'm going to need a new pair of\nshorts...")
	msgbox(format("Shorts are easily an S tier pick for pants!"), MSGBOX_AUTOCLOSE)
}

