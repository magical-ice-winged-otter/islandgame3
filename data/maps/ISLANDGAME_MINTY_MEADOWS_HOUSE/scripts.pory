mapscripts ISLANDGAME_MINTY_MEADOWS_HOUSE_MapScripts {}

// section: interactions

script ISLANDGAME_MINTY_MEADOWS_HOUSE_Interact_Pops {
    lock
    faceplayer
    goto_if_set(FLAG_MINTY_MEADOWS_FREE_MINT, MintRepeatCustomer)

    msgbox(format(
        "Salutations! My name is Pops. Would you like to try one of our Meadow Mints?\p"
        "They can be used to change the nature of your Pokémon!"
    ), MSGBOX_YESNO)

    if (var(VAR_RESULT) == YES) {
        msgbox(format("Good show! Which scent would you like? The first one you get is free!"), MSGBOX_AUTOCLOSE)
        goto(MintShop)
    }
    else {
        msgbox(format("Oh... bad show..."), MSGBOX_AUTOCLOSE)
        release
        end
    }

MintRepeatCustomer:
    msgbox(format("Welcome back, trainer! Here to try another of our fresh mints?"))
    showmoneybox(18, 0)
    msgbox(format("For the price of ¥1000, I will happily sell you one of them!"), MSGBOX_YESNO)

    if (var(VAR_RESULT) == YES) {
        if (checkmoney(1000)) {
            hidemoneybox
            delay(15)
            goto(MintShop)
        }
        else {
            msgbox(format("Oh dear... I'm afraid I can't give you a mint if you don't have enough money."), MSGBOX_AUTOCLOSE)
            hidemoneybox
            release
            end
        }
    }
    else {
        hidemoneybox
        msgbox(format("Oh... bad show..."), MSGBOX_AUTOCLOSE)
        release
        end
    }

MintShop:
    dynmultipush("I'll decide later!", 0)
    dynmultipush("Lonely Mint", ITEM_LONELY_MINT)
    dynmultipush("Adamant Mint", ITEM_ADAMANT_MINT)
    dynmultipush("Naughty Mint", ITEM_NAUGHTY_MINT)
    dynmultipush("Brave Mint", ITEM_BRAVE_MINT)
    dynmultipush("Bold Mint", ITEM_BOLD_MINT)
    dynmultipush("Impish Mint", ITEM_IMPISH_MINT)
    dynmultipush("Lax Mint", ITEM_LAX_MINT)
    dynmultipush("Relaxed Mint", ITEM_RELAXED_MINT)
    dynmultipush("Modest Mint", ITEM_MODEST_MINT)
    dynmultipush("Mild Mint", ITEM_MILD_MINT)
    dynmultipush("Rash Mint", ITEM_RASH_MINT)
    dynmultipush("Quiet Mint", ITEM_QUIET_MINT)
    dynmultipush("Calm Mint", ITEM_CALM_MINT)
    dynmultipush("Gentle Mint", ITEM_GENTLE_MINT)
    dynmultipush("Careful Mint", ITEM_CAREFUL_MINT)
    dynmultipush("Sassy Mint", ITEM_SASSY_MINT)
    dynmultipush("Timid Mint", ITEM_TIMID_MINT)
    dynmultipush("Hasty Mint", ITEM_HASTY_MINT)
    dynmultipush("Jolly Mint", ITEM_JOLLY_MINT)
    dynmultipush("Naive Mint", ITEM_NAIVE_MINT)
    dynmultipush("Serious Mint", ITEM_SERIOUS_MINT)
    dynmultistack(0, 0, TRUE, 5, FALSE, 0, DYN_MULTICHOICE_CB_SHOW_ITEM)

    if (var(VAR_RESULT) == 0) {
        msgbox(format("Alright. Come back when you've made a decision!"), MSGBOX_AUTOCLOSE)
    }
    else {
        giveitem(VAR_RESULT)

        if (flag(FLAG_MINTY_MEADOWS_FREE_MINT)) {
            showmoneybox(18, 0)
            msgbox(format("Jolly good show! Thank you for your patronage!"), MSGBOX_AUTOCLOSE)
            removemoney(1000)
            updatemoneybox
            playse(SE_SHOP)
            waitse
            hidemoneybox
        }
        else {
            setflag(FLAG_MINTY_MEADOWS_FREE_MINT)
            msgbox(format("Jolly good show! I hope your Pokémon likes it!"), MSGBOX_AUTOCLOSE)
        }
    }

    release
}

script ISLANDGAME_MINTY_MEADOWS_HOUSE_Interact_Mordecai {
    playmoncry(SPECIES_STARLY,CRY_MODE_NORMAL)
    msgbox(format("hueheuhuehuehue melk"), MSGBOX_NPC)
}

script ISLANDGAME_MINTY_MEADOWS_HOUSE_Interact_Rigby {
    playmoncry(SPECIES_ZIGZAGOON,CRY_MODE_NORMAL)
    msgbox(format("hueheuhuehuehue cereal"), MSGBOX_NPC)
}

script ISLANDGAME_MINTY_MEADOWS_HOUSE_Interact_Benson {
    playmoncry(SPECIES_DRIFBLIM,CRY_MODE_NORMAL)

    msgbox(format(
        "MORDECAI AND RIGBY GO AND GET US SOME FRESH MINTS OR YOU'RE BOTH FIRED!!!\p"
        "Huh? Oh, sorry. I didn't mean to startle you.\p"
        "I'm Benson, the manager of Minty Meadows. Please enjoy your stay!"
    ), MSGBOX_NPC)
}
