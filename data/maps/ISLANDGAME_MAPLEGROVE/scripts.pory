mapscripts ISLANDGAME_MAPLEGROVE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (gettimeofday == TIME_NIGHT) {
            setobjecthidden(LOCALID_MAZE_BUNEARY)
            setobjecthidden(LOCALID_MAZE_GIRL)
        }
    }
}

// section: interactions

script ISLANDGAME_MAPLEGROVE_Interact_MazeBuneary {
    faceplayer
    playmoncry(SPECIES_BUNEARY,CRY_MODE_NORMAL)
    goto_if_unset(FLAG_MAPLEGROVE_BUNEARY_GIFT, BunearyGift)
    msgbox(format("Konpeko Konpeko Konpeko!"), MSGBOX_AUTOCLOSE)
    end

BunearyGift:
    msgbox(format(
        "{COLOR RED}(Huh? It looks like Buneary is trying to give me something.)\p"
        "(Should I take it?)"
    ), MSGBOX_YESNO)

    if (var(VAR_RESULT) == NO) {
        // nothing to do
    }
    elif (checkitemspace(ITEM_NORMAL_GEM)) {
        giveitem(ITEM_NORMAL_GEM)
        setflag(FLAG_MAPLEGROVE_BUNEARY_GIFT)
    }
    else {
        msgbox(format("{COLOR RED}(I don't have enough space to carry it.)"))
    }

    closemessage
}

script ISLANDGAME_MAPLEGROVE_Interact_MazeMudkip {
    lock
    faceplayer
    playmoncry(SPECIES_MUDKIP,CRY_MODE_NORMAL)
    waitmoncry
    release
}

script ISLANDGAME_MAPLEGROVE_Interact_MazeGirl {
    msgbox(format(
        "Be kind to Pokémon, and they'll be kind to you. \p"
        "That's what my big sister told me. Now I'm friends with all the Pokémon here!"
    ), MSGBOX_NPC)
}

script ISLANDGAME_MAPLEGROVE_Interact_LawnCottonee {
    playmoncry(SPECIES_COTTONEE,CRY_MODE_WEAK)

    msgbox(format(
        "Nee...\p"
        "{COLOR RED}(It seems like Cottonee is trying to tell me something.) \p"
        "(She says her name is CJ and that she hates the Fletchling over there.)\p"
        "(She says it's because she stole her boyfriend from her.)\p"
        "(...How does that even work?)"
    ), MSGBOX_NPC)
}

script ISLANDGAME_MAPLEGROVE_Interact_LawnFletchling {
    playmoncry(SPECIES_FLETCHLING,CRY_MODE_NORMAL)

    msgbox(format(
        "Chirp chirp...\p"
        "{COLOR RED}(It seems like Fletchling is trying to tell me something.) \p"
        "(She says her name is Margaret and that she hates the Cottonee over there.)\p"
        "(She says it's because she stole her boyfriend from her.)\p"
        "(...How does that even work?)"
    ), MSGBOX_NPC)
}

script ISLANDGAME_MAPLEGROVE_Interact_LawnMan {
    if (var(VAR_MINTY_MEADOWS_STATE) != MINTY_MEADOWS_SAVED_MAREEP) {
        msgbox(format(
            "Hrm... Only two of you little ones here today? \p"
            "That pack of savage Mightyena must be frightening the little critters... \p"
            "I do hope something gets done about them. Those feral beasts have been terrorizing this place for long enough!"
        ), MSGBOX_AUTOCLOSE)
    }
    else {
        msgbox(format("These two Pokémon don't get along at all. I don't know why."), MSGBOX_NPC)
    }
}

script ISLANDGAME_MAPLEGROVE_Interact_PondSurskit {
    playmoncry(SPECIES_SURSKIT,CRY_MODE_NORMAL)
    msgbox(format("A"), MSGBOX_NPC)
}

script ISLANDGAME_MAPLEGROVE_Interact_PondLady {
    msgbox(format(
        "Maplegrove has been one with nature since its construction many years ago. \p"
        "Our bond with the Pokémon here is unbreakable."
    ), MSGBOX_NPC)
}

script ISLANDGAME_MAPLEGROVE_Interact_Sign {
    msgbox(format(
        "MAPLEGROVE \n"
        "The Sylvan Sanctuary"
    ), MSGBOX_SIGN)
}

