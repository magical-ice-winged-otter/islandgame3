mapscripts ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        quest_emote_on_warp(LOCALID_BERRY_MANIAC, QUEST_PERSI_BERRY_MANIAC)
    }
}

// section: misc

const VAR_ARG_ITEM_BERRY = VAR_0x8000
const VAR_ARG_ITEM_REWARD = VAR_0x8001
const VAR_ARG_SUBQUEST = VAR_0x8002
const VAR_SUBMITTED_BERRY_COUNT = VAR_0x8003

raw`
.macro trytakeberry subQuestId:req, berryId:req, rewardId:req
setvar VAR_0x8000, \berryId
setvar VAR_0x8001, \rewardId
setvar VAR_0x8002, \subQuestId
call ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_TryTakeBerry
.endm
`

script ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_TryTakeBerry {
    if (checkitem(VAR_ARG_ITEM_BERRY) && !subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_PERSI_BERRY_MANIAC, VAR_ARG_SUBQUEST)) {
        bufferitemname(STR_VAR_1, VAR_ARG_ITEM_BERRY)
        msgbox(format("YES! YES! THE {STR_VAR_1}! HAND IT OVER TO ME RIGHT NOW!!!"), MSGBOX_YESNO)

        if (var(VAR_RESULT) == NO) {
            msgbox(format("WHAT!? YOU DARE DENY ME MY DESTINY, YOU INSOLENT BRAT? GIVE IT TO ME!!!"), MSGBOX_AUTOCLOSE)
        }
        elif (var(VAR_RESULT) == YES) {
            if (!checkitemspace(VAR_ARG_ITEM_REWARD)) {
                msgbox(format("WHAT'S THIS??! YOU HAVE NO ROOM FOR MY {STR_VAR_1}??! THIS SIMPLY WILL NOT DO!"), MSGBOX_AUTOCLOSE)
            }
            else {
                msgbox(format("EXCELLENT, EXCELLENT! HERE IS YOUR REWARD, MY SWEET BERRY GATHERER!"))
                removeitem(VAR_ARG_ITEM_BERRY)
                giveitem(VAR_ARG_ITEM_REWARD)
                subquestmenu(QUEST_MENU_COMPLETE_QUEST, QUEST_PERSI_BERRY_MANIAC, VAR_ARG_SUBQUEST)
                closemessage
            }
        }
    }

    if (subquestmenu(QUEST_MENU_CHECK_COMPLETE, QUEST_PERSI_BERRY_MANIAC, VAR_ARG_SUBQUEST)) {
        addvar(VAR_SUBMITTED_BERRY_COUNT, 1)
    }

    return
}

// section: interactions

script ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_Interact_BerryManiac {
    faceplayer

    switch (returnqueststate(QUEST_PERSI_BERRY_MANIAC)) {
    case QUEST_INACTIVE:
        msgbox(format(
            "I AM THE BERRY MANIAC!!!{PAUSE 30} I AM LOOKING FOR THE RAREST BERRIES IN MARYA!!!\p"
            "BRING THEM TO ME, AND I WILL REWARD YOU HANDSOMELY DEAR FRIEND!"
        ), MSGBOX_AUTOCLOSE)

        startquest(QUEST_PERSI_BERRY_MANIAC)
        goto(TryTakeBerries)

    case QUEST_ACTIVE:
        goto(TryTakeBerries)

    case QUEST_COMPLETED:
        msgbox(format("FINALLY. IT'S COMPLETE. I HAVE ALL THE BERRIES NEEDED FOR MY COLLECTION!!!\pAHAHAHAHAHAHA"), MSGBOX_AUTOCLOSE)
        end
    }

TryTakeBerries:
    msgbox(format("DO YOU POSSESS A BERRY I YEARN FOR!?"), MSGBOX_AUTOCLOSE)
    setvar(VAR_SUBMITTED_BERRY_COUNT, 0)

    trytakeberry(SUB_QUEST_PERSI_BERRY_1, ITEM_LIECHI_BERRY, ITEM_MUSCLE_BAND)
    trytakeberry(SUB_QUEST_PERSI_BERRY_2, ITEM_GANLON_BERRY, ITEM_GRASSY_SEED)
    trytakeberry(SUB_QUEST_PERSI_BERRY_3, ITEM_SALAC_BERRY, ITEM_CHOICE_SCARF)
    trytakeberry(SUB_QUEST_PERSI_BERRY_4, ITEM_PETAYA_BERRY, ITEM_WISE_GLASSES)
    trytakeberry(SUB_QUEST_PERSI_BERRY_5, ITEM_APICOT_BERRY, ITEM_MISTY_SEED)
    trytakeberry(SUB_QUEST_PERSI_BERRY_6, ITEM_KEE_BERRY, ITEM_ELECTRIC_SEED)
    trytakeberry(SUB_QUEST_PERSI_BERRY_7, ITEM_MARANGA_BERRY, ITEM_PSYCHIC_SEED)
    trytakeberry(SUB_QUEST_PERSI_BERRY_8, ITEM_LANSAT_BERRY, ITEM_EXPERT_BELT)
    trytakeberry(SUB_QUEST_PERSI_BERRY_9, ITEM_ENIGMA_BERRY, ITEM_LEFTOVERS)
    trytakeberry(SUB_QUEST_PERSI_BERRY_10, ITEM_MICLE_BERRY, ITEM_CHOICE_SPECS)
    trytakeberry(SUB_QUEST_PERSI_BERRY_11, ITEM_CUSTAP_BERRY, ITEM_CHOICE_BAND)
    trytakeberry(SUB_QUEST_PERSI_BERRY_12, ITEM_JABOCA_BERRY, ITEM_ROCKY_HELMET)
    trytakeberry(SUB_QUEST_PERSI_BERRY_13, ITEM_ROWAP_BERRY, ITEM_ASSAULT_VEST)
    trytakeberry(SUB_QUEST_PERSI_BERRY_14, ITEM_STARF_BERRY, ITEM_LIFE_ORB)

    if (var(VAR_SUBMITTED_BERRY_COUNT) == 14) {
        completequest(QUEST_PERSI_BERRY_MANIAC)
    }
}

script ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_Interact_Woman {
    msgbox(format("Planting berries is really fun.\pIt's like I'm giving gifts to my future self!"), MSGBOX_NPC)
}

script ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_Interact_Shopkeep {
    msgbox(format("Welcome to Berry Good. All of our berries are homegrown and fresh."), MSGBOX_NPC)
    pokemart(ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_Interact_Shopkeep_Mart)
    msgbox(format("May peace be with you."), MSGBOX_AUTOCLOSE)
}

mart ISLANDGAME_CAMP_PERSI_BERRY_HOUSE_Interact_Shopkeep_Mart {
    ITEM_BERRY_JUICE
    ITEM_ORAN_BERRY
    ITEM_PECHA_BERRY
    ITEM_CHERI_BERRY
    ITEM_CHESTO_BERRY
    ITEM_PERSIM_BERRY
}
