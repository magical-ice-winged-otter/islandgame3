mapscripts ISLANDGAME_ORANNA_CHERIPORT_HOUSE2_MapScripts {}

// silverwing man
script ISLANDGAME_ORANNA_CHERIPORT_HOUSE2_NPC1 {
    lock
    faceplayer
    msgbox(format(
        "This building was once a derelict chapel on the brink of collapse. \p"
        "We have since then renovated it and given it a new purpose. \p"
        "To provide beach amenities and complimentary drinks to those in need."
    ))
    closemessage
    release
}

// tuber
script ISLANDGAME_ORANNA_CHERIPORT_HOUSE2_NPC2 {
    lock
    faceplayer
    msgbox(format(
        "I wish I was a Wooper. Then, I wouldn't have to wear this inner tube!"
    ))
    closemessage
    release

}

// silverwing woman
script ISLANDGAME_ORANNA_CHERIPORT_HOUSE2_DrinkGiver {
    lock
    faceplayer
    if (!flag(FLAG_DAILY_ORANNA_CHERIPORT_DRINK)) 
    {
        msgbox(format(
            "You look thirsty. Would you like a refreshing beverage?"
        ), MSGBOX_YESNO)

        if (var(VAR_RESULT) == NO) {
            msgbox(format("We'll always be here if you need a drink."))
            goto(Cheriport_DrinkGiver_End)
        }

        setflag(FLAG_DAILY_ORANNA_CHERIPORT_DRINK)

        // Randomly select some item to give the player.
        random(100)
        if (var(VAR_RESULT) < 30)
        {
            giveitem(ITEM_FRESH_WATER)
        }
        elif (var(VAR_RESULT) < 30 + 25)
        {
            giveitem(ITEM_LEMONADE)
        }
        elif (var(VAR_RESULT) < 30 + 25 + 20)
        {
            giveitem(ITEM_SODA_POP)
        }
        elif (var(VAR_RESULT) < 30 + 25 + 20 + 15)
        {
            giveitem(ITEM_MOOMOO_MILK)
        }
        else
        {
            giveitem(ITEM_BERRY_JUICE)
        }
    }
    else
    {
        msgbox(format("Sorry, our policy is one beverage per day. Come back tomorrow."))
    }

    Cheriport_DrinkGiver_End:
        closemessage
        release
}

