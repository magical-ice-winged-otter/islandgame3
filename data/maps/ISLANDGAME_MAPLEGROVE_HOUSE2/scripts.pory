const FLAG_TEMP_HEARD_MONOLOGUE = FLAG_TEMP_1

mapscripts ISLANDGAME_MAPLEGROVE_HOUSE2_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        quest_emote_on_warp(LOCALID_LOVER, QUEST_MAPLEGROVE_LOVERS)
    }
}

// section: interactions

script ISLANDGAME_MAPLEGROVE_HOUSE2_Interact_Doll {
    msgbox(format("{COLOR RED}(It's a Jigglypuff plush. It's very jiggly.)"), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_MAPLEGROVE_HOUSE2_Interact_Lover {
    switch (returnqueststate(QUEST_MAPLEGROVE_LOVERS)) 
    {
    case QUEST_INACTIVE:
        goto(ISLANDGAME_MAPLEGROVE_HOUSE1_AskForHelp)

    case QUEST_ACTIVE:
        if (checkitem(ITEM_ISLANDGAME_JULIES_LETTER)) {
            goto(ISLANDGAME_MAPLEGROVE_HOUSE1_QuestCompleted)
        }
        else {
            msgbox(format("My love is somewhere in Applevine. Please tell me what she says!"), MSGBOX_NPC)
        }

    case QUEST_REWARD:
        goto(ISLANDGAME_MAPLEGROVE_HOUSE1_TryGiveReward)

    case QUEST_COMPLETED:
        msgbox(format("I guess I was being a little dramatic earlier."), MSGBOX_NPC)
    }
}

script ISLANDGAME_MAPLEGROVE_HOUSE1_AskForHelp {
    if (!flag(FLAG_TEMP_HEARD_MONOLOGUE)) {
        setobjectmovementtype(LOCALID_LOVER, MOVEMENT_TYPE_FACE_LEFT)
        removeobject(LOCALID_LOVER)
        addobject(LOCALID_LOVER)

        msgbox(format(
            "Oh Julie, my dear Julie! Why has fate whisked you away from me?\p"
            "Tis been but only a month since you departed from Oranna...\p"
            "But it feels as if a millennium has come and gone!\p"
            "Oh! If only I had mustered the courage to confess my feelings sooner!\p"
            "If only I had not wasted our last week together before you moved to Lumine...\p"
            "Then, maybe then, the power of our newfound love would have bound us together on this island forever!\p"
            "But alas, the great Maryan Sea now separates us for eternity...\p"
            "Oh great winged guardian of the sea! Hear my lowly plea!\p"
            "Bestow upon me a messenger of love! One that may deliver my confession to my one true soulmate in this world!\p"
            "Perhaps... a traveling Pok√©mon trainer of sorts!\p"
            "Preferably, one that will happen to pass by the town of Applevine!\p"
            "...{PAUSE 60}Eh?"
        ), MSGBOX_AUTOCLOSE)

        faceplayer
        playse(SE_PIN)
        applymovement(LOCALID_LOVER, moves(emote_exclamation_mark, delay_8 * 6))
        waitmovement
        setflag(FLAG_TEMP_HEARD_MONOLOGUE)
    }

    faceplayer

    msgbox(format(
        "O-Oh! You must be the messenger of love that Lugia has blessed me with!\p"
        "Please! Deliver this letter to my beloved! It is all I ask for in my life!"
    ), MSGBOX_YESNO)

    if (var(VAR_RESULT) == YES) {
        msgbox(format("Splendid! My love is somewhere in Applevine. Please tell me what she says!"), MSGBOX_AUTOCLOSE)
        giveitem(ITEM_ISLANDGAME_ROMANS_LETTER)
        startquest(QUEST_MAPLEGROVE_LOVERS)
    }
    else {
        msgbox(format("Oh... My apologies. I mistook you for a heavenly messenger sent by Lugia."), MSGBOX_AUTOCLOSE)
    }
}

script ISLANDGAME_MAPLEGROVE_HOUSE1_QuestCompleted {
    faceplayer
    removeitem(ITEM_ISLANDGAME_JULIES_LETTER)

    msgbox(format(
        "Ah! You've returned! What did she say?\p"
        "{COLOR RED}(You hand Julie's letter to Roman.)\p"
        "{COLOR DARK_GRAY}...{PAUSE 60}Oh...{PAUSE 60}I see...{PAUSE 60}What a fool I truly was...{PAUSE 60}\p"
        "...It appears I've misinterpreted her leaving to be permanent.\p"
        "Turns out she's only staying there for the summer to help her relatives with their family business.\p"
        "Come to think of it... it did seem strange that her parents were still living here...\p"
        "My apologies, trainer. Please take this as penance for my blunder."
    ), MSGBOX_AUTOCLOSE)

    goto(ISLANDGAME_MAPLEGROVE_HOUSE1_TryGiveReward)
}

script ISLANDGAME_MAPLEGROVE_HOUSE1_TryGiveReward {
    if (checkitemspace(ITEM_DESTINY_KNOT)) {
        giveitem(ITEM_DESTINY_KNOT)

        msgbox(format(
            "Hm? You wish to know her response to my confession?\p"
            "I suppose I do owe it to you to at least tell you what she said...\p"
            "She told me to meet her at the Silverwing Festival for her answer!\p"
            "Argh... the anticipation is killing me...{PAUSE 30} but I am at least glad to know I'll still be able to see her, regardless of her answer.\p"
            "It just gives us more time to form a bond together!"
        ), MSGBOX_AUTOCLOSE)

        completequest(QUEST_MAPLEGROVE_LOVERS)
    }
    else {
        questmenu(QUEST_MENU_SET_REWARD, QUEST_MAPLEGROVE_LOVERS)
        applymovement(LOCALID_LOVER, moves(emote_exclamation_mark))

        msgbox(format(
            "Oh, you don't have room in your bag for my Destiny Knot...\p"
            "But don't worry. Come back when you have room and it'll be yours!"
        ), MSGBOX_AUTOCLOSE)
    }
}
