mapscripts ISLANDGAME_FRESA_FARMS_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        // This hides the rescued mareep until we actually rescue them
        if (var(VAR_MINTY_MEADOWS_STATE) != MINTY_MEADOWS_SAVED_MAREEP) {
            setobjecthidden(LOCALID_MAREEP_RESCUED)
            setobjecthidden(LOCALID_MAREEP_TOUGH)
        }

        if (gettimeofday == TIME_NIGHT) {
            setobjectxyperm(LOCALID_MILTANK, 18, 6)
            setobjectxyperm(LOCALID_TORCHIC, 14, 9)
            setobjectxyperm(LOCALID_TEPIG, 16, 8)
            setobjectmovementtype(LOCALID_TORCHIC, MOVEMENT_TYPE_WANDER_AROUND)
            setobjectmovementtype(LOCALID_TEPIG, MOVEMENT_TYPE_WANDER_AROUND)
            setobjectmovementradius(LOCALID_TORCHIC, 1, 1)
            setobjecthidden(LOCALID_MILK_VENDOR)
            setobjecthidden(LOCALID_TEPIG_BOY)
            setobjecthidden(LOCALID_DUDE)
            setobjecthidden(LOCALID_FIDOUGH_GIRL)
        }
    }
}

// section: interactions

script ISLANDGAME_FRESA_FARMS_Interact_FidoughGirl {
    if (flag(FLAG_FRESA_FARMS_TRADE_DONE)) {
        msgbox(format(
            "Fidough are a real gift from Arceus to us farmers!"
            "When they eventually grow up into Daschbuns, their sweet scent helps our wheat grow up real fast n' good!"
        ), MSGBOX_NPC)
        end
    }

    if (checkpartymon(SPECIES_FIDOUGH)) {
        goto(FidoughRequestTrade)
    }

    msgbox(format(
        "Fidough are just the cutest li'l doggos in the whole wide world!\p"
        "I wish I had one of my own, but they're just so gosh-darn hard to find, and I'm always too busy with chores to go out and look fer one. Oh hum..."
    ), MSGBOX_NPC)
    end

FidoughRequestTrade:
    lock
    faceplayer
    playse(SE_PIN)
    applymovement(LOCALID_FIDOUGH_GIRL, moves(emote_exclamation_mark, delay_16 * 2))
    waitmovement

    msgbox(format(
        "Ooooo! Is that an sweet widdle Fidough I see?\p"
        "Pleeeeeease trade me it! I got another adorable puppy to make it worth yer while!"
    ), MSGBOX_YESNO)

    if (var(VAR_RESULT) == YES) {
        switch (trade(INGAME_TRADE_RIOLU)) {
        case TRADE_CANCELED:
            msgbox(format("Aw, shucks."), MSGBOX_AUTOCLOSE)
        case TRADE_WRONG_SPECIES:
            msgbox(format("Eeeeh? That ain't no Fidough."), MSGBOX_AUTOCLOSE)
        case TRADE_SUCCESS:
            msgbox(format("Eeeeee! 'Preciate your kindness, trainer! Hope ye like yer new pet as well. She may look small, but she's a real tough boxer!"), MSGBOX_AUTOCLOSE)
            setflag(FLAG_FRESA_FARMS_TRADE_DONE)
        }
    }
    else {
        msgbox(format("Aw, shucks."), MSGBOX_AUTOCLOSE)
    }

    release
}

script ISLANDGAME_FRESA_FARMS_Interact_MilkVendor {
    lock
    faceplayer
    msgbox(format("Our milk is sourced from the healthiest Miltank in all of Marya, Lady Moona!"))
    showmoneybox(0,0)
    msgbox(format("We are selling it in packs of six for only ¥7000! Would you like a pack?{PAUSE 30}"), MSGBOX_YESNO)

    if (var(VAR_RESULT) == YES) {
        if (!checkmoney(7000)) {
            msgbox(format("Oh dear, you don't have enough money, sweetie."), MSGBOX_AUTOCLOSE)
            hidemoneybox
        }
        else {
            removemoney(7000)
            updatemoneybox
            playse(SE_SHOP)
            waitse
            hidemoneybox
            giveitem(ITEM_MOOMOO_MILK, 6)
            msgbox(format("Thank you for your patronage!"), MSGBOX_AUTOCLOSE)
        }
    }
    else {
        hidemoneybox
    }

    release
}

script ISLANDGAME_FRESA_FARMS_Interact_MareepRescued {
    lock
    faceplayer
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    goto_if_unset(FLAG_FRESA_FARMS_MAREEP_GIFT, MareepGiveGift)
    msgbox(format("Reep. {COLOR BLUE}(I found it wandering around today. Hope it helps you out!)"), MSGBOX_AUTOCLOSE)
    release
    end

MareepGiveGift:
    msgbox(format(
        "Reep! {COLOR BLUE}(Hey there! Thanks for saving my butt from that scary Mightyena.)\p"
        "{COLOR DARK_GRAY}Reep. {COLOR BLUE}(Please take this as a gift.)"
    ))

    if (checkitemspace(ITEM_ELECTRIC_GEM)) {
        setflag(FLAG_FRESA_FARMS_MAREEP_GIFT)
        giveitem(ITEM_ELECTRIC_GEM)
    }
    else {
        msgbox(format("Reep. {COLOR BLUE}(Oh, you don't have enough space for this.)"))
    }

    closemessage
    release
}

script ISLANDGAME_FRESA_FARMS_Interact_MareepDogWatching {
    lock
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)

    if (gettimeofday == TIME_NIGHT) {
        msgbox(format(
            "Reeep. {COLOR BLUE}(Heh. That dumb dog's snoozing on his job again!)\p"
            "{COLOR DARK_GRAY}Reep reep. {COLOR BLUE}(Time to sneak out and have some fun with Chansey...)\p"
            "{COLOR DARK_GRAY}Reep? {COLOR BLUE}(Huh?)"
        ), MSGBOX_AUTOCLOSE)

        applymovement(LOCALID_MAREEP_DOGWATCHING, moves(face_player))
        applymovement(LOCALID_MAREEP_DOGWATCHING, moves(emote_exclamation_mark))
        waitmovement
        playse(SE_PIN)
        waitse

        msgbox(format(
            "Reep... {COLOR BLUE}(Oh, it's just a random kid. Good thing they can't understand me.)\p"
            "{COLOR DARK_GRAY}Reep! {COLOR BLUE}(Go away! I've got big plans for tonight!)\p"
            "{COLOR DARK_GRAY}Reep. {COLOR BLUE}(Oh wait... They don't understand me.)"
        ), MSGBOX_AUTOCLOSE)

        applymovement(LOCALID_MAREEP_DOGWATCHING, moves(face_original_direction))
        waitmovement
    }
    else {
        msgbox(format(
            "Reep... {COLOR BLUE}(This mutt is a pain in my fluffy behind...)\p"
            "{COLOR DARK_GRAY}Reep. {COLOR BLUE}(I got important business to attend to at night!)"
        ), MSGBOX_AUTOCLOSE)
    }

    release
}

script ISLANDGAME_FRESA_FARMS_Interact_MareepTough {
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)

    msgbox(format(
        "Reep. {COLOR BLUE}(I'm just saying man, if it was me against that Mightyena, hoo boy.)\p"
        "{COLOR DARK_GRAY}Reep! {COLOR BLUE}(I would'a messed that Mightyena up real good, ya feel me?)\p"
        "{COLOR DARK_GRAY}Reep? {COLOR BLUE}(Huh? Why are you looking me like that? I'm fo' realzies!)"
    ), MSGBOX_NPC)
}

script ISLANDGAME_FRESA_FARMS_Interact_Tepig {
    lock

    if (gettimeofday != TIME_NIGHT) {
        msgbox(format("{COLOR RED}(Tepig is busy chowing down a bowl of well-done berries.)"), MSGBOX_AUTOCLOSE)
    }
    else {
        faceplayer
        playmoncry(SPECIES_TEPIG, CRY_MODE_NORMAL)
        waitmoncry
    }

    release
}

script ISLANDGAME_FRESA_FARMS_Interact_Miltank {
    if (gettimeofday == TIME_NIGHT) {
        msgbox(format("{COLOR RED}(Miltank is fast asleep.)"), MSGBOX_AUTOCLOSE)
    }
    else {
        faceplayer
        playmoncry(SPECIES_MILTANK, CRY_MODE_NORMAL)
        msgbox(format("Moo. {COLOR BLUE}(Moo.)"), MSGBOX_NPC)
    }
}

script ISLANDGAME_FRESA_FARMS_Interact_Chansey {
    playmoncry(SPECIES_CHANSEY, CRY_MODE_NORMAL)

    msgbox(format(
        "Chansey. {COLOR BLUE}(Most humans can't understand us, but some are born with a special ability to comprehend what Pokémon are saying.)\p"
        "{COLOR DARK_GRAY}Chansey. {COLOR BLUE}(You're one of them,{PAUSE 30} aren't you?)"
    ), MSGBOX_NPC)
}

script ISLANDGAME_FRESA_FARMS_Interact_Mareep {
    lock
    faceplayer
    playmoncry(SPECIES_MAREEP, CRY_MODE_NORMAL)
    waitmoncry
    release
}

script ISLANDGAME_FRESA_FARMS_Interact_Torchic {
    lock
    faceplayer
    playmoncry(SPECIES_TORCHIC, CRY_MODE_NORMAL)
    waitmoncry
    release
}

script ISLANDGAME_FRESA_FARMS_Interact_Mudsdale {
    playmoncry(SPECIES_MUDSDALE, CRY_MODE_NORMAL)
    msgbox(format("Hee-haw! {COLOR BLUE}(Hee-ho!)"), MSGBOX_NPC)
}

script ISLANDGAME_FRESA_FARMS_Interact_TepigBoy {
    msgbox(format(
        "By breeding two Pokémon in the same family together, sometimes its child will inherit one of its parents' moves!\p"
        "These inherited moves are called egg moves, and most of them can't be learned any other way!\p"
        "My Tepig knows a “super powerful” move thanks to its mom! Isn't that awesome?"
    ), MSGBOX_NPC)
}

script ISLANDGAME_FRESA_FARMS_Interact_Dude {
    msgbox(format("I wonder what came first... the Pokémon, or the egg?"), MSGBOX_NPC)
}

script ISLANDGAME_FRESA_FARMS_Interact_Sign {
    msgbox(format(
        "FRESA FARMS\n"
        "Paulie and Marion"
    ), MSGBOX_SIGN)
}
