mapscripts ISLANDGAME_CAMP_PERSI_MACHETE_TREE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        // TODO(poe): Is this needed? Or are these automatically cleared?
        //setvar(VAR_TEMP_0, 0)
    }

    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: ISLANDGAME_CAMP_PERSI_TREEHOUSE_ReturnFromHell
    ]
}

// section: util

script ISLANDGAME_CAMP_PERSI_TREEHOUSE_ReturnFromHell {
    setvar(VAR_TEMP_0, 1)
    msgbox("hi")

    if (checkitem(ITEM_ISLANDGAME_STRANGE_DISC) && flag(FLAG_ANOMALY01_DEFEATED)) {
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
        waitmovement
        delay(60)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_question_mark))
        waitmovement

        msgbox(format(
            "...\p"
            "{COLOR RED}(Augh... my head...)\p"
            "(What in the world was all of that? Was it all just a dream?)\p"
            "(Seems like the program's deleted itself as well...)"
        ),MSGBOX_AUTOCLOSE)

        removeitem(ITEM_ISLANDGAME_STRANGE_DISC)
    }
}

// section: interactions

script ISLANDGAME_CAMP_PERSI_TREEHOUSE_Interact_Laptop {
    if (flag(FLAG_ANOMALY01_DEFEATED)) {
        msgbox(format("> You try to open “LostSilver.exe”, but it just results in an error."), MSGBOX_AUTOCLOSE)
    }
    else {
        msgbox(format(
            "It's a dusty old laptop. It looks like it's been here for years.\p"
            "There seems to still be some power in it. You could probably still use it."
        ), MSGBOX_AUTOCLOSE)

        if (checkitem(ITEM_ISLANDGAME_STRANGE_DISC)) {
            msgbox(format("Insert the strange disc into the laptop?"), MSGBOX_YESNO)

            if(var(VAR_RESULT) == YES) {
                closemessage
                special(Script_FadeOutMapMusic)
                waitstate

                msgbox(format(
                    "> You booted up the laptop and inserted the disc.\p"
                    "“LostSilver.exe” is automatically installed and opened.\p"
                    "It's loading...{PAUSE 60} loading...{PAUSE 60} loading...{PAUSE 60}\p"
                    "{COLOR RED}(Huh? It seems like the laptop froze while attempting to run the game.)"
                ), MSGBOX_AUTOCLOSE)

                applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_exclamation_mark))
                setvar(VAR_RESULT, 0)
                playse(SE_ORB)
                special(DoOrbEffect)
                delay(300)
                special(FadeOutOrbEffect)
                applymovement(OBJ_EVENT_ID_PLAYER, spookySpin)
                waitmovement
                warp(MAP_ISLANDGAME_ANOMALY01_1F, 8, 15)
            }
        }
    }
}

script ISLANDGAME_CAMP_PERSI_TREEHOUSE_Interact_TV {
    msgbox(format(
        "It's a really old television set riddled with cobwebs.\p"
        "You tried to turn it on, but nothing happened."
    ), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_CAMP_PERSI_TREEHOUSE_Interact_GulpinDoll {
    if (var(VAR_CAMP_PERSI_STATE) != CAMP_PERSI_SEARCHING_FOR_CARD) {
        msgbox(format("{COLOR RED}(It's just a regular old Gulpin doll.)"), MSGBOX_AUTOCLOSE)
    }
    else {
        msgbox(format(
            "{COLOR RED}(It's just a regular old Gulpin doll.)\p"
            "(Oh? There seems to be something stashed inside it...)"
        ), MSGBOX_AUTOCLOSE)

        setflag(FLAG_FOLLOWERS_DISABLED)
        hidefollower
        delay(10)
        playse(SE_EXIT)
        addobject(LOCALID_TREE_CARD_LOSER)
        setobjectxy(LOCALID_TREE_CARD_LOSER, 4, 4)
        delay(30)
        applymovement(LOCALID_TREE_CARD_LOSER, moves(emote_question_mark))
        waitmovement
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_down))
        waitmovement
        delay(60)
        msgbox(format("Huh? What are you doing here?"), MSGBOX_AUTOCLOSE)
        delay(60)
        playse(SE_PIN)
        applymovement(LOCALID_TREE_CARD_LOSER, moves(emote_exclamation_mark, walk_in_place_faster_up))
        waitmovement
        delay(60)
        msgbox(format("H-Hey! You were snooping around in my secret base, weren't you!?"), MSGBOX_AUTOCLOSE)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_down))
        waitmovement
        delay(60)

        msgbox(format(
            "Mrgrgr... {PAUSE 30}Fine! You got me! I stole it! I'm the one who took that camper's card!\p"
            "It's not fair! I've been using up my allowance for WEEKS trying to get one, and he pulls it from his FIRST pack?\p"
            "A card with THAT much power shouldn't be allowed! There's no reason a newbie should be able to beat me so easily!\p"
            "I'm just leveling the playing field, is all! You understand, right!?\p"
            "...\p"
            "...Now that you know the truth, you're gonna tattle on me, aren't you?"
        ), MSGBOX_AUTOCLOSE)

        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_down))
        waitmovement
        delay(60)
        applymovement(LOCALID_TREE_CARD_LOSER, moves(emote_question_mark))
        waitmovement

        msgbox(format(
            "H-Huh? You'll let me go... if I just beat you in a Pokémon battle?\p"
            "Heh... I guess have no choice but to oblige.\p"
            "I'll show you the power of my bug Pokémon!"
        ), MSGBOX_AUTOCLOSE)

        applymovement(LOCALID_TREE_CARD_LOSER, moves(walk_up))
        waitmovement
        delay(30)
        trainerbattle_no_intro(TRAINER_CAMP_PERSI_THIEF, "T-This isn't right! I should've won!")
        applymovement(LOCALID_TREE_CARD_LOSER, moves(face_up, lock_facing_direction, walk_fast_down))
        waitmovement
        delay(60)

        msgbox(format(
            "I don't get it... My Pokémon should've won! They're much stronger then yours!\p"
            "Is...{PAUSE 30} power not all there is to battling? Can weaker Pokémon still beat stronger Pokémon with the right strategy?"
        ), MSGBOX_AUTOCLOSE)

        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_in_place_down))
        waitmovement
        delay(60)

        msgbox(format(
            "I... I see. That's what you were trying to teach me with that battle, wasn't it?\p"
            "To show me how dumb it was of me to get so worked up over a trading card...\p"
            "Heh... I sure feel like a real big stupid jerk now.\p"
            "Well, I guess it's time for me pay for my crimes..."
        ), MSGBOX_AUTOCLOSE)

        delay(60)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up, delay_16 * 4, face_down, walk_in_place_down))
        waitmovement

        msgbox(format(
            "Huh? You won't tell on me? B-But, I lost...\p"
            "...\p"
            "O-Okay. Thank you so much! I won't forget your kindness, trainer! I promise to never ever steal again!"
        ), MSGBOX_AUTOCLOSE)

        applymovement(LOCALID_TREE_CARD_LOSER, moves(face_down, unlock_facing_direction, walk_down))
        waitmovement
        playse(SE_EXIT)
        waitse
        setobjecthidden(LOCALID_TREE_CARD_LOSER)
        setvar(VAR_CAMP_PERSI_STATE, CAMP_PERSI_DEFEATED_LOSER)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up, delay_16 * 4, walk_in_place_faster_up, delay_16 * 4))
        waitmovement
        giveitem(ITEM_ISLANDGAME_TRADING_CARD)
        clearflag(FLAG_FOLLOWERS_DISABLED)
        closemessage
    }
}
