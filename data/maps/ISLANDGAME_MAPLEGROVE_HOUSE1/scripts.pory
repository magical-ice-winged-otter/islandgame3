mapscripts ISLANDGAME_MAPLEGROVE_HOUSE1_MapScripts {}

// section: interactions

script ISLANDGAME_MAPLEGROVE_HOUSE1_Interact_HPTutor {
    lock
    faceplayer

    msgbox(format(
        "Hmm... Your Pokémon possess a dormant power within them... \p"
        "Hidden potential just waiting to be unlocked... \p"
        "I can help them obtain this hidden power... provided you have the funds..."
    ))

    showmoneybox(0, 0)
    msgbox(format("I will teach your Pokémon its hidden power for ¥1500... How's that sound?"), MSGBOX_YESNO)
    hidemoneybox

    if (var(VAR_RESULT) == NO) {
        msgbox(format("What a shame... All that hidden potential gone to waste."))
    } 
    elif (checkmoney(1500)) {
        msgbox(format("Excellent... Whose hidden power do you want unlocked...?"))
        setvar(VAR_0x8005, MOVE_HIDDEN_POWER)
        special(ChooseMonForMoveTutor)
        waitstate

        if (var(VAR_RESULT) != 0) {
            showmoneybox(0, 0)
            msgbox(format("Your Pokémon's hidden power has been unlocked... I will take my payment now."))
            closemessage
            removemoney(1500)
            updatemoneybox
            playse(SE_SHOP)
            waitse
            hidemoneybox
        }
        else {
            msgbox(format("What a shame... All that hidden potential gone to waste."))
        }
    }
    else {
        msgbox(format("My services do not come cheap... Come back when you have sufficient funds."))
    }

    release
}

script ISLANDGAME_MAPLEGROVE_HOUSE1_Interact_Togepi {
    playmoncry(SPECIES_TOGEPI,CRY_MODE_NORMAL)

    msgbox(format(
        "Togepi! {COLOR BLUE}(Hello hooman!) \p"
        "{COLOR DARK_GRAY}Togi-togi! {COLOR BLUE}(Wow! You can understand me? That's amazing!) \p"
        "{COLOR DARK_GRAY}Pi-pi. {COLOR BLUE}(You must be really special.)"
    ), MSGBOX_NPC)
}

script ISLANDGAME_MAPLEGROVE_HOUSE1_Interact_TogepiMan {
    msgbox(format("My Togepi is very talkative. I wish I could understand what he was saying..."), MSGBOX_NPC)
}

