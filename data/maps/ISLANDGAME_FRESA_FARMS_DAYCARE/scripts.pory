mapscripts ISLANDGAME_FRESA_FARMS_DAYCARE_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        quest_emote_on_warp(LOCALID_TORCHIC_BREEDER, QUEST_FRESA_FARMS_TORCHIC_EGG)

        // the breeder (paulie) only appears after the minty mareep quest
        if (var(VAR_MINTY_MEADOWS_STATE) != MINTY_MEADOWS_SAVED_MAREEP) {
            setobjecthidden(LOCALID_TORCHIC_BREEDER)
        }
    }
}

// section: interactions

script ISLANDGAME_FRESA_FARMS_DAYCARE_Interact_TorchicBreeder {
    lock
    faceplayer

    if (specialvar(VAR_RESULT, GetDaycareState) == DAYCARE_EGG_WAITING) {
        goto(Route117_EventScript_DaycareEggWaiting)
    }

    switch (returnqueststate(QUEST_FRESA_FARMS_TORCHIC_EGG)) {
    case QUEST_INACTIVE:
        msgbox(format(
            "{COLOR GREEN}Paulie: {COLOR DARK_GRAY}Ah, young trainer! It's good to see you again! \p"
            "I actually have another favor I wish to ask of you. Fret not, it is a relatively simple and easy one! \p"
            "I have a very special egg that I'd like you to take along on your journey! \p"
            "All I ask is for you to help me hatch and show it to me later. Is that alright?"
        ) ,MSGBOX_YESNO) 

        switch (var(VAR_RESULT)) {
        case TRUE:
            msgbox(format("Excellent! The Pokémon will be yours to keep, of course!"))
            giveegg(SPECIES_TORCHIC, NUM_NATURES, 2, 18, 18, 18, 18, 18, 18)
            startquest(QUEST_FRESA_FARMS_TORCHIC_EGG)
        case FALSE:
            msgbox(format("Ah, I understand. If you ever change your mind though, I'll always be here!"))
        }

    case QUEST_ACTIVE:
        if (checkpartymon(SPECIES_BLAZIKEN)) {
            msgbox(format(
                "{COLOR GREEN}Paulie: {COLOR DARK_GRAY}Young trainer! It's been quite a while! Since you're here, I assume your Torchic has matured into a fully-fledged Blaziken now, hm? \p"
                "Wow... Magnificent. Simply magnificent. Quite the majestic Pokémon, isn't it? \p"
                "Well, a promise is a promise! Here is something I've been holding onto for a few years, just waiting for the right trainer to give it to, and I see no better recipient than you!"
            ))

            giveitem(ITEM_BLAZIKENITE)

            msgbox(format(
                "{COLOR GREEN}Paulie: {COLOR DARK_GRAY}You certainly are something special, young trainer. It hadn't even occurred to me that I never even learned your name.\p"
                "Ah... {PLAYER} is it? Well, you have my sincerest of gratitudes, {PLAYER}.\p"
                "I wish you the best of luck on the rest of your adventure. If you ever need a place to stay, our gate is always open."
            ))

            completequest(QUEST_FRESA_FARMS_TORCHIC_EGG)
        }
        elif (checkpartymon(SPECIES_COMBUSKEN)) {
            msgbox(format(
                "{COLOR GREEN}Paulie: {COLOR DARK_GRAY}Waha, that little Torchic has grown up into a Combusken already?\p"
                "Childhood really just zooms right by, doesn't it? I'm looking forward to seeing it fully evolved!"
            ))
        }
        elif (checkpartymon(SPECIES_TORCHIC)) {
            msgbox(format(
                "{COLOR GREEN}Paulie: {COLOR DARK_GRAY}Oh! It finally hatched? Let me take a closer look at it... \p"
                "Ah, so it hatched into a Torchic? Welcome to parenthood, young trainer! \p"
                "That Torchic you have is no ordinary one either. It has a hidden ability! \p"
                "Take good care of Torchic! If you come back to me when it's fully evolved, I'll give you a little something special!"
            ))
        }
        else {
            msgbox(format("{COLOR GREEN}Paulie: {COLOR DARK_GRAY}Let me know what the egg hatches into later, young trainer!"))
        }

    case QUEST_COMPLETED:
        msgbox(format("{COLOR GREEN}Paulie: {COLOR DARK_GRAY}I wish you the best of luck on the rest of your adventure. If you ever need a place to stay, our gate is always open."))
    }

    release
}

