mapscripts ISLANDGAME_CAMP_PERSI_LODGE_1_MapScripts {
    MAP_SCRIPT_ON_RESUME {
        call(Common_TryRemoveTemporaryObject)
    }

    MAP_SCRIPT_ON_TRANSITION {
        setobjecthidden(LOCALID_LODGE_PORYGON)

        // the kids only play during the day if the card is returned and the rewards have been given
        if (returnqueststate(QUEST_PERSI_FAVORITE_CARD) != QUEST_COMPLETED || var(VAR_CAMP_PERSI_STATE) != CAMP_PERSI_CLAIMED_LOSER_REWARD || gettimeofday == TIME_NIGHT) {
            setobjecthidden(LOCALID_LODGE_CARD_KID)
            setobjecthidden(LOCALID_LODGE_CARD_LOSER)
        }

        if (gettimeofday == TIME_NIGHT) {
            setobjecthidden(LOCALID_LODGE_GAMER_RIGHT)
            setobjecthidden(LOCALID_LODGE_GAMER_MIDDLE)
            setobjecthidden(LOCALID_LODGE_GAMER_LEFT)
        }
    }    
}

// section: util

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_BattleSpeedMenu {
    message(format("> CHANGE BATTLE SCENE SPEED"))
    waitmessage
    dynmultipush("x1 (Default)", OPTIONS_BATTLE_SCENE_1X)
    dynmultipush("x2", OPTIONS_BATTLE_SCENE_2X)
    dynmultipush("x3", OPTIONS_BATTLE_SCENE_3X)
    dynmultipush("x4", OPTIONS_BATTLE_SCENE_4X)
    dynmultistack(0, 0, FALSE, 4, FALSE, NO, DYN_MULTICHOICE_CB_NONE)

    if (var(VAR_RESULT) == VAR_BATTLE_SPEED) {
        msgbox(format("> SPEED IS ALREADY SET TO THIS!"), MSGBOX_AUTOCLOSE)
    }
    elif (var(VAR_RESULT) != MULTI_B_PRESSED) {
        copyvar(VAR_BATTLE_SPEED, VAR_RESULT)
        playmoncry(SPECIES_MAGNEZONE, CRY_MODE_HIGH_PITCH)
        msgbox(format("> SPEED SET!"), MSGBOX_AUTOCLOSE)
    }
    else {
        closemessage
    }
}

// section: interactions

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Interact_Scoutmaster {
    if (gettimeofday == TIME_NIGHT) {
        msgbox(format(
            "I wish parents would stop dropping their brats here like it's a daycare.\p"
            "This is a CAMP for Lugia's sake!"
        ), MSGBOX_NPC)
    }
    else {
        msgbox(format(
            "Where did that rapscallion Hanzo run off to now!?\p"
            "He better not be horsing around with that machete of his again. I don't care if it's an “agricultural tool”, there's no reason a CHILD should have one!!\p"
            "-sigh- I'm starting to think I was better off in the slammer than here..."
        ), MSGBOX_NPC)
    }
}

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Interact_GamerRight {
    msgbox(format("Give me the controller! The scoutmaster said it's MY turn now!"), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Interact_GamerMiddle {
    msgbox(format("Quit bothering me! I'm in an intense game right now!"), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Interact_GamerLeft {
    msgbox(format("Dude stop spamming that move!!! -.-"), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Interact_CardPlayerLeft {
    msgbox(format("Charizard! Use Fire Spin!"), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Interact_CardPlayerRight {
    msgbox(format("Ugh, if I only had one more turn! I would have won!"), MSGBOX_AUTOCLOSE)
}

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Interact_TV {
    goto_if_set(FLAG_CAMP_PERSI_PORYGON_DEFEATED, ISLANDGAME_CAMP_PERSI_LODGE_1_1F_BattleSpeedMenu)
    msgbox(format("{COLOR RED}(...Huh?)"), MSGBOX_AUTOCLOSE)
    delay(30)
    playse(SE_THUNDER)
    fadescreen(FADE_TO_WHITE)   delay(15)
    fadescreen(FADE_FROM_WHITE) delay(15)
    fadescreen(FADE_TO_BLACK)   delay(15)
    fadescreen(FADE_FROM_BLACK) delay(15)
    setobjectvisible(LOCALID_LODGE_PORYGON)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(lock_facing_direction, walk_down, unlock_facing_direction))
    waitmovement
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, moves(emote_exclamation_mark))
    waitmovement
    playmoncry(SPECIES_PORYGON, CRY_MODE_ENCOUNTER)
    waitmoncry
    applymovement(OBJ_EVENT_ID_PLAYER, moves(face_up))
    waitmovement
    setflag(FLAG_SMART_WILD_AI)
    setflag(FLAG_NO_CATCHING)
    setflag(FLAG_NO_RUNNING)
    setflag(FLAG_SYS_CTRL_OBJ_DELETE)
    setvar(VAR_BATTLE_SPEED, OPTIONS_BATTLE_SCENE_4X)
    setvar(VAR_LAST_TALKED, LOCALID_LODGE_PORYGON)

    createmon(B_SIDE_OPPONENT, B_FLANK_LEFT, SPECIES_PORYGON, 10,
        ITEM_BERRY_JUICE, ITEM_POKE_BALL, NUM_NATURES, 1, MON_GENDERLESS,
        252, 252, 252, 252, 252, 252,
        31, 31, 31, 31, 31, 31,
        MOVE_RECYCLE, MOVE_CONVERSION_2, MOVE_TACKLE, MOVE_THUNDER_SHOCK)

    dowildbattle
    setvar(VAR_BATTLE_SPEED, OPTIONS_BATTLE_SCENE_1X)
    clearflag(FLAG_SMART_WILD_AI)
    clearflag(FLAG_NO_CATCHING)
    clearflag(FLAG_NO_RUNNING)
    clearflag(FLAG_SYS_CTRL_OBJ_DELETE)

    if (specialvar(VAR_RESULT, GetBattleOutcome) == B_OUTCOME_WON) {
        setflag(FLAG_CAMP_PERSI_PORYGON_DEFEATED)
        goto(ISLANDGAME_CAMP_PERSI_LODGE_1_1F_BattleSpeedMenu)
    }
}

// section: triggers

script ISLANDGAME_CAMP_PERSI_LODGE_1_1F_Trigger_BlockingTV {
    if (gettimeofday != TIME_NIGHT) {
        playse(SE_PIN)
        applymovement(LOCALID_LODGE_GAMER_LEFT, moves(emote_exclamation_mark))
        applymovement(LOCALID_LODGE_GAMER_MIDDLE, moves(emote_exclamation_mark))
        waitmovement
        delay(48)
        msgbox(format("MOVE YOUR BIG FAT HEAD OUT OF THE FREAKING WAY!"), MSGBOX_AUTOCLOSE)
        applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_right * 2, face_left))
        waitmovement
    }
}
